% tipasym4.mf: TIPA phonetic symbols (4) non-IPA, old IPA symbols
% Copyright 1996-2003 FUKUI Rei
%
% This program may be distributed and/or modified under the
% conditions of the LaTeX Project Public License, either version 1.2
% of this license or (at your option) any later version.
% The latest version of this license is in
%   http://www.latex-project.org/lppl.txt
% and version 1.2 or later is part of all distributions of LaTeX 
% version 1999/12/01 or later.
%
% This program consists of all files listed in Manifest.txt.
%
% Version 1.2 2003/01/01
%
% This font is based on:
%   Computer Modern font series by D. E. Knuth and
%   TSIPA by KOBAYASHI Hajime, FUKUI Rei and SHIRAKAWA Shun.
%

cmchar "Crossed B";
beginchar(oct"240",10u#+serif_fit#,asc_height#,0);
italcorr .5x_height#*slant+min(.5curve#-u#,-.25u#);
adjust_fit(serif_fit#,0);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0l=z0'l; x0'=x1; x0=x2;
lft x1l=hround(side_gap-.5stem'); top y1=h;
numeric edge; edge=rt x2r;
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,180);
pos4(vair,90); pos5(curve,0); pos6(vair,-90); penpos7(x3l-x3r,-180);
rt x3l=max(rt x3l-(lft x3r-tiny.lft x2l),1/3[rt x2,edge]);
y3=1/8[bar_height,x_height];
x4l=w-.5(w-serif_fit)+.5u; top y4r=x_height+oo;
rt x5r=hround min(w-1.35u+.5curve,w-.6u); y5=.5x_height;
x6l=x4l-.2u; bot y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=min(x,.5[x4,x5r]);
(x',y6r)=whatever[z7l,z6l]; x6r:=min(x',.5[x6,x5r]);
filldraw stroke z3e{up}...pulled_arc.e(4,5)&pulled_arc.e(5,6)...{up}z7e; % bowl
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{right}z4l));
y2=ypart(((edge,h)--(edge,0))intersectionpoint(z6l{left}...{up}z7l));
pickup tiny.nib; filldraw stroke z1e--z0'e--z0e--z2e;  % stem
pickup crisp.nib; pos8(hair,0); pos7'(stem,0);
z7'=z2; x8l=x7'l; bot y8=0;
filldraw stroke z7'e--z8e;  % point
if serifs: sloped_serif.l(1,0',a,1/3,jut,serif_drop); fi  % upper serif
pickup if serifs: tiny.nib else: fine.nib fi;
hbar(9,10,lft x0l-hround(1.8u+serif_fit),rt x0r+hround(side_gap),
 x_height+.5(h-x_height));
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "Crossed D";
beginchar(oct"241",10u#+serif_fit#,asc_height#,0);
italcorr asc_height#*slant-serif_fit#+.5stem#-.5u#;
adjust_fit(0,serif_fit#);
d_stroke(true,true,0);
pickup if serifs: tiny.nib else: fine.nib fi;
hbar(8,9,lft x0l-hround 2.5u,rt x0r+hround(1.8u+serif_fit),
 x_height + .5(h-x_height));
penlabels(0,1,2,3,4,5,6,7); endchar;

cmchar "Curly-tail D";
beginchar(oct"242",10u#+serif_fit#,asc_height#,desc_depth#);
italcorr asc_height#*slant-serif_fit#+.5stem#-2.25u#;
adjust_fit(0,serif_fit#+u#);
d_stroke(true,false,.45bar_height);
if serifs: pickup tiny.nib; else: pickup fine.nib; fi
pos2'(stem,0); z2'=z2; numeric curlytail[];
curlytail1=if serifs: vair; else: vair; fi
curlytail2=if serifs: hair; else: .8hair; fi
curlytail3=if serifs: vair; else: vair; fi
pos10(curlytail1,90); x10=.4[x2r,x11]; bot y10l=if serifs: -oo; else: -o; fi
pos11(curlytail2,180); rt x11=w+.5u; y11=.3bar_height;
pos12(curlytail3,280); x12=x2r+.5u; y12=2y11;
pos13(hair,360); x13=.4w; y13=-.75desc_depth;
filldraw stroke z2'e{down}..z10e..{up}z11e...
 {(-3,-1)}z12e...{(-1,-2)}z13e; % tail
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13); endchar;

cmchar "Hooktop right-tail D";
beginchar(oct"243",10u#+serif_fit#,asc_height#,desc_depth#);
italcorr asc_height#*slant+.5u#;
adjust_fit(0,serif_fit#+1.75u#);
d_stroke(false,false,0);
hooktop(0,9,10,11,stem,hround(w+1.5u),h,.9,.6,.5);
if serifs: right_tail(2,12,13,14,stem,hround(w+1u),.88,.5,1/3);
else: right_tail(2,12,13,14,stem,hround(w+1u),.833,.5,1/3); fi
penlabels(0,1,2',3,4,5,6,7,9,10,11,12,13,14); endchar;

cmchar "The letter g";
beginchar(oct"245",CT(9u#,10u#),x_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(0,0);
numeric light_vair,light_curve,loop_top,loop_side;
light_vair=Vround(.5[thin_join,vair]+vair_corr);
light_curve=max(fine.breadth,hround(curve-3stem_corr));
loop_top=Vround .77[vair,fudged.stem];
loop_side=hround .64[vair,fudged.stem];
pickup fine.nib; pos1(light_vair,90);
pos2(light_curve,180); pos3(light_vair,270);
pos4(light_curve,360); pos11(loop_top,90);
y11r=good.y(y11r+.15bar_height-y11); x1=x3=.5[x2,x4]; y2=y4=.5[y1,y3];
lft x2r=hround(1.75u-.5light_curve); rt x4r=hround(w-2.75u+.5light_curve);
top y1r=h+oo; y3r=good.y(y3r+.27[top y11r,bot y1l]-y3);
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3);  % left half of bowl
filldraw stroke pulled_arc.e(3,4) & pulled_arc.e(4,1);  % right half of bowl
x0=superness[x1r,x4r]; y0=superness[y4r,y1r];  % NE point on the super bowl
x8'=superness[x3r,x2r]; y8'=superness[y2r,y3r];  % SW point on the super bowl
x8''=superness[x3,x2]; y8''=superness[y2,y3];
pos0(thin_join,angle(z0-z8')+90);
pos8(thin_join,angle(z0-z8')-90); z8=.618[z8',z8''];
if serifs: pos5(vair,90);
 pos6(hair,0); pos7(.5[hair,flare],0); x5=.75[x0,x6]; top y5r=h+o;
 rt x6r=hround(w-.25u); y6+(.5[hair,flare])/2=.95[bar_height,h]+oo;
 filldraw stroke z0e..{right}z5e; bulb(5,6,7);  % ear
else: pos5(vair',100); top y5r=h+oo;
 rt x5l=hround(w-.25u); y5l:=good.y y5l;
 filldraw z0l{z5-z0}..z5l--z5r{left}..{curl 1}z0r--cycle; fi  % ear
pos10(loop_top,90); x10=x8+.75u; y10=y11;
pos12(loop_side,0); pos13(light_vair,-90);
pos14(loop_side,-180); pos10'(.5[thin_join,vair],-270);
rt x12r=hround max(w-1.25u+.5loop_side,w-.5u); y12=y14=.5[y11,y13];
x11=x13=max(.5w,x10+eps); bot y13r=-d-oo-1; x14=w-x12; z10'l=z10l;
filldraw stroke z8e{z8-z0}..z10e---z11e;  % link
filldraw stroke {{interim superness:=hein_super; super_arc.e(11,12)}}
 & super_arc.e(12,13) & super_arc.e(13,14) & super_arc.e(14,10');  % loop
penlabels(0,1,2,3,4,5,6,7,8,10,11,12,13,14); labels(8',8''); endchar;

cmchar "Left-hooktop Long I";
beginchar(oct"246",CT(6u#,6.66u#),x_height#,desc_depth#);
italcorr x_height#*slant-2u#;
adjust_fit(0,serif_fit#);
pickup tiny.nib; pos3(stem,0); x2=x3; bot y3=-d;
x0=.75u; lft x2l = hround(.5w+.5u-.5stem); hook_in(0,1,2);
filldraw stroke z2e--z3e;
dish_serif(3,2,a,1/3,jut,b,1/3,jut);
penlabels(0,1,2,3); endchar;

cmchar "Viby I";
beginchar(oct"247",CT(7.1u#,7.78u#),x_height#,desc_depth#);
italcorr x_height#*slant-2u#;
adjust_fit(if monospace:.5u#,.5u# else:0,-.5u# fi);
pickup tiny.nib; x0=.75u;
lft x2l = hround(.5w-.5stem); x3=x2; x5=w-.75u;
hook_in(0,1,2); hook_out_bot(3,4,5);
filldraw stroke z2e--z3e;
penlabels(0,1,2,3,4,5); endchar;

cmchar "Turned K";
beginchar(oct"251",CT(9.5u#,10u#),x_height#,desc_depth#);
italcorr x_height#*slant;
adjust_fit(serif_fit#,serif_fit#); pickup tiny.nib;
numeric right_jut,stem[],alpha[];
stem1=max(tiny.breadth,hround(fudged.stem-stem_corr));
stem2=max(tiny.breadth,hround(fudged.stem-2stem_corr));
stem3=max(tiny.breadth,hround(fudged.hair if hefty:-\\4stem_corr fi));
stem4=max(tiny.breadth,hround(fudged.stem-3stem_corr));
if serifs: right_jut=.6jut; else: right_jut=.4tiny; fi
pos1(stem1,0); pos2(stem2,0); top y1=x_height+d+min(oo,serif_drop); bot y2=0;
lft x1l=lft x2l=hround(side_gap-.5stem1);
top y3=x_height; rt x3r=hround(r-letter_fit-.7u-right_jut)+eps;
bot y6=0; rt x6r=hround(r-letter_fit-.3u-right_jut)+eps;
x4=x11=x1; y4=.7bar_height; y11=y3;
alpha1=diag_ratio(1,.5(stem3-tiny),y3-y4,x3r-x4);
alpha2=diag_ratio(1,.5(stem4-tiny),y11-y6,x6r-x1);
penpos3(alpha1*(stem3-tiny),0); penpos4(whatever,-90);
z5=.5[z5l,z5r]; penpos6(alpha2*(stem4-tiny),0);
forsuffixes $=l,r: y3'$=x_height; y6'$=0; z4$=z3'$+whatever*(z3-z4);
 z5$=z6'$+whatever*(z11-z6)=whatever[z3,z4]; endfor
z3'r=z3r+penoffset z3-z4 of currentpen+whatever*(z3-z4);
% we have also z3'l=z3l+penoffset z4-z3 of currentpen+whatever*(z3-z4);
z6'r=z6r+penoffset z11-z6 of currentpen+whatever*(z11-z6);
z6'l=z6l+penoffset z6-z11 of currentpen+whatever*(z11-z6);
fill z4r--diag_end(4r,3'r,1,.5,3'l,4l)--z4l--cycle;  % upper diagonal
fill z5l--diag_end(5l,6'l,.5,1,6'r,5r)--z5r--cycle;  % lower diagonal
pos0(stem1,0); pos0'(stem2,0); y0=y0'; x0=x1; x0'=x2;
rt z0r=whatever[z3,z4]; filldraw stroke z1e..z0e--z0'e..z2e; % stem
if serifs: numeric inner_jut;
 if rt x2r+jut+.5u+1<=lft x6l-jut: inner_jut=jut;
 else: rt x2r+inner_jut+.5u+1=lft x6l-inner_jut; fi
 sloped_serif.l(1,0,a,1/3,jut,serif_drop); % upper stem serif
 dish_serif(2,0',b,1/3,jut,c,1/3,inner_jut);  % lower stem serif
 dish_serif(3,4,d,2/3,1.4jut,e,1/2,right_jut)(dark); % upper diagonal serif
 dish_serif(6,5,f,1/2,inner_jut,g,1/3,right_jut)(dark);fi % lower diagonal serif
turn_picture_lower; penlabels(0,1,2,3,4,5,6,11); endchar;

cmchar "Barred L";
beginchar(oct"252",7u#,asc_height#,0);
italcorr asc_height#*slant-1u#;
adjust_fit(0,0);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.5w-.5stem'); x1=x2; top y1=h; bot y2=0;
filldraw stroke z1e--z2e;  % stem
if serifs: sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % upper serif
 dish_serif(2,1,b,1/3,jut,c,1/3,jut); fi  % lower serif
hbar(3,4,lft x1l - hround(2u),rt x2r + hround(2u), .82x_height+.5bar);
penlabels(1,2); endchar;

cmchar "Lambda";
beginchar(oct"253",CT(9.5u#,10u#),asc_height#,0);
adjust_fit(0,0); pickup fine.nib;
pos1(vair,90); lft x1=hround .1u; top y1r=h;
x2=x1+2u; y2=.7[x_height,h]; x3=w-2u; bot y4=-oo; y3=max(.07h,y4+eps);
z4-(.25u,0)=whatever[z2,z3]; numeric theta; theta=angle(z2-z3)-90;
pos2(stem,theta); pos3(stem,theta); pos4(stem,0);
filldraw circ_stroke z1e{right}...z2e---z3e
 ...{2(x4e-x3e),y4e-y3e}z4e;  % long diagonal
y5=.5[bar_height,x_height]; z5=whatever[z2,z3];
x6=1.5u; y6-.5stem=-oo; pos5(hair,theta-90); pos6(stem,angle(z5-z6)-90);
filldraw circ_stroke z6e--z5e; % short diagonal
penlabels(1,2,3,4,5,6); endchar;

cmchar "Crossed lambda";
beginchar(oct"254",CT(9.5u#,10u#),asc_height#,0);
adjust_fit(0,0); pickup fine.nib;
pos1(vair,90); lft x1=hround .1u; top y1r=h;
x2=x1+2u; y2=.7[x_height,h]; x3=w-2u; bot y4=-oo; y3=max(.07h,y4+eps);
z4-(.25u,0)=whatever[z2,z3]; numeric theta; theta=angle(z2-z3)-90;
pos2(stem,theta); pos3(stem,theta); pos4(stem,0);
filldraw circ_stroke z1e{right}...z2e---z3e
 ...{2(x4e-x3e),y4e-y3e}z4e;  % long diagonal
y5=.5[bar_height,x_height]; z5=whatever[z2,z3];
x6=1.5u; y6-.5stem=-oo; pos5(hair,theta-90); pos6(stem,angle(z5-z6)-90);
filldraw circ_stroke z6e--z5e; % short diagonal
z7=whatever[z2,z3]; y7=1/3[y5,y2]; pos7(stem,theta+90);
z8=z7+whatever*dir theta; x8=1.5u-.25stem; 
z9=z7+whatever*dir theta; x9-x7=x7-x8;
pos8(.6[vair,stem],theta+90); pos9(.6[vair,stem],theta+90);
filldraw stroke z8e--z9e;
penlabels(1,2,3,4,5,6); endchar;

cmchar "Old L-Yogh Ligature";
beginchar(oct"255",CT(9.5u#,10u#),asc_height#,desc_depth#);
italcorr x_height#*slant-.5serif_fit#-u#;
adjust_fit(serif_fit#+stem_shift#,.5serif_fit#);
pickup fine.nib;
pos1(if serifs: vair else: stem' fi,180);
lft x1r=hround(side_gap-.5stem'); y1=.68x_height;
pos2(.3[hair,stem],90); x2=.5w+u; top y2r=x_height+o;
pos3(stem,0); x3=x7-.5u; y3=.4[bar_height,x_height];
pos5(.7[hair,curve],0); x5r=.7[x2,x3]; y5=.7bar_height;
pos6(.8[hair,curve],30); x6=.5[x5l,x7r]; y6=.45[y5,y7];
pos7(curve,0); rt x7r=hround(w-.75u); y7=0;
filldraw stroke z1e{up}...{right}z2e...{down}z3e...{down}z5e
 ...z6e...{down}z7e; % bowl
if serifs: pickup tiny.nib;
 x8=x7; bot y8=-.16d; pos8(curve,0);
 pos9(vair,-90); pos10(hair,-180); pos10'(flare,-180);
 x9=.5[x8,x10r]; bot y9r=-d-oo; y10-.5flare=-.7d;
 z10r=z10'r; lft x10'r=hround 2u; 
 numeric xx;
 (xx,y9r)=whatever[z9l,z8l]; x9r:=max(xx,.5[x10r,x9]);
 filldraw stroke z7e--z8e{down}...z9e{left}; bulb(9,10,10'); % arc and bulb
else: pickup fine.nib;
 x8=x7; bot y8=-.16d; pos8(curve,0);
 pos9(vair,-90); x9=.5[x8,x10r]; bot y9r=-d-o;
 pos10(.7[vair,flare],-130); lft x10r=hround 2u;
 y10r=good.y -.7d; y10l:=good.y y10l;
 filldraw stroke z7e--z8e & super_arc.e(8,9)
  & term.e(9,10,left,.9,4); fi
pickup tiny.nib; pos51(stem',0); pos52(stem',0); % l
if odd(w-stem'): change_width; fi
lft x51l=hround(side_gap-.5stem'); x51=x52; top y51=h; bot y52=0;
filldraw stroke z51e--z52e;  % stem
if serifs: sloped_serif.l(51,52,e,1/3,jut,serif_drop);  % upper serif
 dish_serif(52,51,f,1/3,jut,g,1/3,jut); fi  % lower serif
penlabels(1,2,3,4,5,6,7,8,9,10,12,13,51,52); endchar;

cmchar "Curly-tail N";
beginchar(oct"256",10u#,x_height#,desc_depth#);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#+u#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
lft x1l=hround(side_gap-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=h+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
raised_h_stroke(2,a,3,4);  % arch and right stem
if serifs: sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); fi % lower left serif
if serifs: pickup tiny.nib; else: pickup fine.nib; fi
pos4'(stem,0); z4'=z4; numeric curlytail[];
curlytail1=if serifs: vair; else: vair; fi
curlytail2=if serifs: hair; else: .8hair; fi
curlytail3=if serifs: vair; else: vair; fi
pos10(curlytail1,90); x10=.4[x4r,x11]; bot y10l=if serifs: -oo; else: -o; fi
pos11(curlytail2,180); rt x11=w+.5u; y11=.3bar_height;
pos12(curlytail3,280); x12=x4r+.5u; y12=2y11;
pos13(hair,360); x13=.45w; y13=-.75desc_depth;
filldraw stroke z4'e{down}  % stem
 ..z10e..{up}z11e...{(-3,-1)}z12e...{(-1,-2)}z13e; % tail
penlabels(1,2,3,4,10,11,12,13); endchar;

cmchar "Open O-E Ligature";
beginchar(oct"257",13u#,x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(0,0);
numeric left_curve,right_curve;
if monospace: right_curve=left_curve=fudged.stem;
else: left_curve=max(fine.breadth,hround(curve-2stem_corr));
 right_curve=max(tiny.breadth,hround(curve-if serifs:6 else:8 fi\\stem_corr));fi
pickup tiny.nib; pos11(right_curve,0);
pos12(vair,90); pos13(mfudged.stem,180);
y11=good.y bar_height; top y12r=h+vround 1.5oo; y10l=bot y11;
rt x11r=hround min(w-.5u,w-u+.5right_curve);
lft x13r=hround (.5(w)-.5mfudged.stem); x12=.55[x13,x11];
{{interim superness:=more_super;
 filldraw stroke super_arc.e(11,12)}};  % right bowl of e
y13=.5[y12,y14]; bot y14r=-oo; x14=x12+.25u;
if serifs: pos14(vair',270); pos15(mfudged.hair,360);
 y15=max(good.y(.5bar_height-.9),y14l+vair); x15r=x11r;
 (x,y14l)=whatever[z14r,z15]; x14l:=min(x,x14l+.5u);
 filldraw stroke pulled_arc.e(12,13)
  & pulled_super_arc.e(13,14)(.8superpull)
  ...{x15-x14,5(y15-y14)}z15e; % left bowl, arc, and terminal of e
else: pos14(vair,270);
 filldraw stroke super_arc.e(12,13)
  & super_arc.e(13,14);  % left bowl and arc of e
 pickup fine.nib; pos14'(vair,270); z14=z14';
 pos15(.5[vair,flare],275); rt x15r=hround(w-.6u);
 y15r=good.y(y15r+1/3bar_height-y15); y15l:=good.y y15l; x15l:=good.x x15l;
 filldraw stroke term.e(14',15,right,1,4); fi  % terminal
y11'r=y10r=y10l+.6[thin_join,vair]; y11'l=y10l; x11'l=x11'r=x11; x10l=x10r=x13;
fill stroke z10e--z11'e;  % crossbar
numeric BH; BH=if Times_Compat:.5613h else: bar_height fi; % Open O
pickup fine.nib; pos2(vair',-90); pos4(vair',90);
pos3(mfudged.stem,0); z3=z13;
x2=x4=.5[.5u,x3]; bot y2r=vround(-1.5oo); top y4r=h+oo;
if serifs: pos1(hair,-180); pos0(flare,-180);
 y1=max(h-BH-.5flare-2vair'-2,h-.9[BH,h]+.5flare);
 lft x1r=hround(.7u); bulb(2,1,0);  % bulb
 pos5(hair,-180); lft x5r=hround(.5u);
 y5=min(h-good.y(.5BH-.9),y4l-vair');
filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  ..tension .9 and 1..{x5-x4,5(y5-y4)}z5e;  % arc and lower terminal
else: pos1(4/7[vair',flare],-100);
 lft x1r=hround(.6u); bot y1r=h-vround .82[BH,top y4r];
 filldraw stroke term.e(2,1,left,.8,4);  % upper terminal
 pos5(.6[vair',flare],95); lft x5r=hround(.5u);
 y5r=good.y(h+y5r-1/3BH-y5);
 y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  ..tension .9 and 1..z5e; fi  % arc and lower terminal
penlabels(1,2,3,4,10,11,12,13,14,15); endchar;

cmchar "Omega";
beginchar(oct"260",CT(11u#,12u#),x_height#,0); % 2003/03/16
italcorr .5x_height#*slant;
adjust_fit(0,0); pickup fine.nib;
pos2(curve,-180); lft x2r=hround.5u; y2=y8=.45x_height;
y4=y6=vround.6h; y1r=h+oo; x1=x5=.5w; pos1(vair,90);
pos3(vair,-90); pos4(hair,0); pos5(vair,90);
pos6(hair,180); pos7(vair,270); pos8(curve,360);
bot y3r=bot y7r=-oo; top y5r=vround(.1[y4,h]+.5vair);
x3=.5[x2,x4]; x7=.5[x6,x8]; rt x8r=hround(w-.5u);
x5=.5[x4,x6]; rt x4r-lft x6r=min(stem,2hair)+2eps;
if x4l<x6l: x4l:=x6l:=x5; fi
pos9(vair,110);pos10(vair,70);
y9r=y10r=h; x9=w-x10=x2+1/2(x1-x2);
filldraw stroke z9e{dir210}...z2e{down}...z3e{right}
 ...{up}z4e...{left}z5e;  % left arc
filldraw stroke z5e{left}...z6e{down}...z7e{right}
 ...{up}z8e...z10e{dir150};
penlabels(1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "Curly-tail Esh";
beginchar(oct"262",CT(5u#,5.56u#),asc_height#,desc_depth#);
italcorr asc_height#*slant+1.5u#;
adjust_fit(.5u# if monospace:+1.5u# fi,.5u# if monospace:+1.5u# fi);
pickup fine.nib; pos1(stem',0); pos2(stem',0);
lft x1l=hround(.5w-.5stem'); x2=x1; y2=0; h-y1=d;
filldraw stroke z1e--z2e; % stem
hooktop(1,3,4,5,stem',w+1u,h,.9,.5,1/3);
if serifs: pickup tiny.nib; else: pickup fine.nib; fi
pos2'(stem,0); z2'=z2; numeric curlytail[];
curlytail1=if serifs: vair; else: vair; fi
curlytail2=if serifs: hair; else: .8hair; fi
curlytail3=if serifs: vair; else: vair; fi
pos7(curlytail1,-90); pos8(curlytail2,-180);
pos9(curlytail3,-270); pos10(hair,0);
bot y7r=-d-o; x7=.45[x8,x2]; lft x8r=hround -1u; y8=-.7d;
z9=(.5w,-.2d); rt x10r=w+.25hair; y10=-d-apex_o;
filldraw stroke z2e{down}...{left}z7e...z8e
 ...z9e...{down}z10e;  % lower stem
penlabels(1,2,3,4,5,7,8,9,10); endchar;

cmchar "Left-hook T";
beginchar(oct"263",CT(6u#+max(u#,.5stem#),5.56u#),
 min(asc_height#,if hefty:9/7 else:10/7 fi\\ x_height#),desc_depth#);
italcorr x_height#*slant if serifs: -.9u# else: -.4u# fi;
adjust_fit(0,if serifs: .5u# else: 0u# fi);
numeric shaved_stem; shaved_stem=hround(stem if hefty:-\\2stem_corr fi);
pickup fine.nib; pos2(shaved_stem,180);pos3(shaved_stem,180);
lft x2r=lft x3r=hround(side_gap-.5shaved_stem);
y2=y8; y3=max(.5bar_height,2vair);
pickup crisp.nib; pos8(bar,90);
rt x8=hround(w- if Times_Compat: .4u else: 1.3u fi);
top y8r=x_height; lft x7=hround 1/3u; y7l=y8l;
if hefty: pos7(bar,90);
 filldraw stroke z7e--z8e;  % crossbar
 pickup tiny.nib; pos1(hround(shaved_stem-stem_corr),0);
 rt x1r=fine.rt x2l; top y1=h;
 penpos2'(x1r-x1l,0); x2'=x1; y2'=y2;
 filldraw stroke z1e--z2'e;  % upper terminal
else: pos7(vair,90); pos1(hair,0);
 rt x1r=fine.rt x2l; top y1=h;
 filldraw z1l{down}...{left}z7r--z7l--z8l
  --z8r--(x1r,y8r)--z1r--cycle; fi  % upper terminal and crossbar
pickup fine.nib; interim superness:=more_super;
pos4(vair',-90); bot y4r=-oo; rt x5r=hround(w- if Times_Compat: 0 fi u);
if serifs: pos5(hair,0); x4l=.5[x3l,x5l];
 y5= if Times_Compat: .5 else: 1 fi [0,y3];
 (x,y4r)=whatever[z4l,z5l]; x4r:=max(x,.5[x3r,x4]);
 filldraw stroke z2e..super_arc.e(3,4)...{up}z5e;  % stem and hook
 if Times_Compat: else:
  pickup crisp.nib; pos6(hair,0); pos5'(hair,0);
  x6=x5=x5'; top y6=max(vround .75bar_height,top y5); y5=y5';
  filldraw stroke z5'e--z6e; fi % terminal
 left_tail(5,10,11,12,hair,hround(.5[x3r,x3]));
else: pos5(vair,-75); top y5l=vround .2[top y4l,bar_height];
 x5l:=good.x x5l; x4l=1/3[x3l,x5l]; x4r:=1/3[x3r,x5r]; y3l:=y3l+.2vair;
 filldraw stroke z2e..super_arc.e(3,4);  % stem and hook
 path p; p=stroke z4e{right}..tension .9 and atleast 1..z5e;  % terminal
 if (xpart(z5l-precontrol 1 of p)<0) or (xpart(z5r-postcontrol 2 of p)<0):
  filldraw stroke z4e{right}...{up}z5e;
 else: filldraw p; fi;
 pos9(.8hair,0); x9r=x5r; y9=y5r;
 left_tail(9,10,11,12,.8hair,hround(.5[x3r,x3])); fi
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;

cmchar "Curly-tail T";
beginchar(oct"264",6u#+max(u#,.5stem#),
 min(asc_height#,if hefty:9/7 else:10/7 fi\\ x_height#),0);
italcorr x_height#*slant if serifs: -.9u# else: -.4u# fi;
adjust_fit(0,if serifs: 0 else: -.5u# fi);
numeric shaved_stem; shaved_stem=hround(stem if hefty:-\\2stem_corr fi);
pickup fine.nib; pos2(shaved_stem,180);pos3(shaved_stem,180);
lft x2r=lft x3r=hround(side_gap-.5shaved_stem); y2=y8;
y3=if serifs: max(.5bar_height,2vair); else: max(.3bar_height,1.5vair); fi
pickup crisp.nib; pos8(bar,90);
rt x8=hround(w-1.3u); top y8r=x_height; lft x7=hround 1/3u; y7l=y8l;
if hefty: pos7(bar,90);
 filldraw stroke z7e--z8e;  % crossbar
 pickup tiny.nib; pos1(hround(shaved_stem-stem_corr),0);
 rt x1r=fine.rt x2l; top y1=h;
 penpos2'(x1r-x1l,0); x2'=x1; y2'=y2;
 filldraw stroke z1e--z2'e;  % upper terminal
else: pos7(vair,90); pos1(hair,0);
 rt x1r=fine.rt x2l; top y1=h;
 filldraw z1l{down}...{left}z7r--z7l--z8l
  --z8r--(x1r,y8r)--z1r--cycle; fi  % upper terminal and crossbar
pickup fine.nib; interim superness:=more_super;
pos4(vair',-90); bot y4r=if serifs: -oo; else: -o; fi
rt x5r=hround(w-u+.25hair); x4l=.6[x3,x5];
pos5(if serifs:hair else: .8hair fi,0);
y5=if serifs: .35bar_height; else: .8[0,y3]; fi
(x,y4r)=whatever[z4l,z5l]; x4r:=max(x,.5[x3r,x4]);
pos11(if serifs: hair else: vair' fi,110); x11=x4-.5u; top y11r=2y5;
pos12(hair,180); x12=0; y12=-.75desc_depth;
filldraw stroke z2e..super_arc.e(3,4)...{up}z5e  % stem and hook
 ...{(-3,-1)}z11e...{(-1,-4)}z12e;
penlabels(1,2,3,4,5,6,7,8,10,11,12); endchar;

cmchar "T-S Ligature";
beginchar(oct"265",12u#,10/7x_height#,0);
italcorr x_height#*slant if serifs: -.75u# else: -.25u# fi;
adjust_fit(0,if serifs: 0 else: -.5u# fi);
numeric shaved_stem; shaved_stem=hround(stem if hefty:-\\2stem_corr fi); % t
pickup fine.nib; pos2(shaved_stem,180);pos3(shaved_stem,180);
lft x2r=lft x3r=hround(side_gap-.5shaved_stem);
y2=y8; y3=max(.5bar_height,2vair);
pickup crisp.nib; pos8(bar,90);
rt x8=hround5u; top y8r=x_height; lft x7=hround 1/3u; y7l=y8l;
if hefty: pos7(bar,90);
 filldraw stroke z7e--z8e;  % crossbar
 pickup tiny.nib; pos1(hround(shaved_stem-stem_corr),0);
 rt x1r=fine.rt x2l; top y1=h;
 penpos2'(x1r-x1l,0); x2'=x1; y2'=y2;
 filldraw stroke z1e--z2'e;  % upper terminal
else: pos7(vair,90); pos1(hair,0);
 rt x1r=fine.rt x2l; top y1=h;
 filldraw z1l{down}...{left}z7r--z7l--z8l
  --z8r--(x1r,y8r)--z1r--cycle; fi  % upper terminal and crossbar
pickup fine.nib; interim superness:=more_super;
pos4(vair',-90); bot y4r=-oo; rt x5r=hround7u;
pos5(hair,0); y5=.35bar_height; x4l=.5[x3l,x5l];
(x,y4r)=whatever[z4l,z5l]; x4r:=max(x,.5[x3r,x4]);
filldraw stroke z2e..super_arc.e(3,4); % stem
numeric x_org, theta; x_org=5u; % s
 theta=90-angle(40u,x_height); slope:=-x_height/40u;  % angle at middle
numeric s_slab; s_slab=if serifs:vair else:Vround .1[vair,stem] fi;
numeric ess'; ess'=max(fine.breadth,ess);
pickup fine.nib; pos12(max(fine.breadth,s_slab-vround vair_corr),-100);
pos10(ess',theta); pos17(vair',-90); x12l=x10=x17=x_org+.5(w-x_org);
top y12l=x_height+vround 1.5oo; bot y17r=-oo;
y10-.5ess'=y17l+if serifs:.54 else: .52 fi\\(y12r-y17l-ess');
lft x13l=hround(x_org+.6u); rt x16r=hround(w-.6u);
x13r-x13l=x16r-x16l=hround .5[s_slab,ess']-fine;
ellipse_set(12l,13l,14l,10l); ellipse_set(12r,13r,14r,10r); y13=y13r;
ellipse_set(17l,16l,15l,10l); ellipse_set(17r,16r,15r,10r); y16=y16r;
interim superness:=more_super;
filldraw stroke super_arc.e(12,13) & z13e{down}
 ..z14e---z15e..z16e{down} & super_arc.e(16,17)..z4e;  % main stroke
if serifs: pos11(hair,180); rt x11l=hround(w-1.05u);
 bot y11=min(bot y12r,vround 1/5[top y15r,x_height]);
 filldraw stroke z11e{up}....{left}z12e;  % upper arc
 path upper_arc; upper_arc=z11{up}....{left}z12;
 pos20(.3[fine.breadth,cap_hair],0);
 x20r=x11l; top y20=top y12l; x11l-x11'=1.6cap_curve-fine; y11'=y11;
 numeric t; t=xpart(upper_arc intersectiontimes(z20l--z11'));
 filldraw z11l--z20r--z20l--subpath(t,0) of upper_arc--cycle;  % upper barb
else: pos11(4/7[s_slab,flare],-100);
 x11l=good.x(x11l+w-u-rt x11); top y11l=vround(.93x_height+1.5oo);
 filldraw stroke term.e(12,11,right,.9,4); fi % upper arc and terminal
penlabels(1,2,3,4,5,6,7,8);
penlabels(10,11,11',12,13,14,15,16,20); endchar;

cmchar "Long Viby Y";
beginchar(oct"266",10u#,x_height#,desc_depth#);
italcorr x_height#*slant-.25u#;
adjust_fit(0,0); interim superness:=more_super;
pickup fine.nib;
lft x0=hround .25u; lft x2l=hround(2.75u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pickup tiny.nib; pos2'(stem,-180); z2'=z2; x3=x2; pos3(stem,-180);
lft x8l=hround(w-side_gap-.5stem); bot y8=-d;
pos4(vair,-90); pos5(hair,0); pos6(stem,0); pos8(stem,0);
x4=.5[x3,x5]; bot y4r=-oo; y3=.7[y4,y5];
x5=x6=x8; y5=.57h; top y6=h;
filldraw stroke z2'e{-u,-x_height}
  ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw stroke z6e--z8e;
if serifs: dish_serif(6,8,a,1/3,jut,b,1/3,jut);
 dish_serif(8,6,c,1/3,jut,d,1/3,jut); fi
penlabels(0,1,2,3,4,5,6,8); endchar;

cmchar "Viby Y";
beginchar(oct"267",10u#,x_height#,desc_depth#);
italcorr x_height#*slant-.25u#;
adjust_fit(0,0); interim superness:=more_super;
pickup fine.nib;
rt x10=hround(w+.25u); lft x8l=hround(w-side_gap-.5stem);
hook_out_bot(8,9,10);
lft x0=hround .25u; lft x2l=hround(2.75u-.5stem);
hook_in(0,1,2)(skewed);  % opening hook
pickup tiny.nib; pos2'(stem,-180); pos8'(stem,0);
z2'=z2; z8'=z8; x3=x2; pos3(stem,-180);
pos4(vair,-90); pos5(hair,0); pos6(stem,0);
x4=.5[x3,x5]; bot y4r=-oo; y3=.7[y4,y5];
x5=x6=x8; y5=.57h; top y6=h;
filldraw stroke z2'e{-u,-x_height}
  ...super_arc.e(3,4)...{up}z5e; % left stem and arc
filldraw stroke z6e--z8'e;
if serifs: dish_serif(6,8,a,1/3,jut,b,1/3,jut); fi
penlabels(0,1,2,3,4,5,6,8,9,10); endchar;

cmchar "Curly-tail Yogh";
beginchar(oct"270",CT(8u#,8.88u#),x_height#,desc_depth#);
italcorr x_height#*slant-.5serif_fit#-.3u#;
adjust_fit(0,.5serif_fit#);
yogh_stroke(.5u,.3,.75u,false,true); % yogh
pos9(vair,-180); pos21(vair,-270);
if serifs: pos22(hair,0); else: pos22(hair,0); fi
x21=.5(w-u); y21=0; rt x22r=w; y22=-d-apex_o; x9=u; y9=.5[y8,y21];
filldraw stroke z8e{left}...{up}z9e...{right}z21e..{(2,-10)}z22e; % curly-tail
penlabels(1,2,3,4,5,6,7,8,9,12,21,22); endchar;

%%%%% not yet good; to be corrected!!!
cmchar "Reversed Yogh";
beginchar(oct"271",CT(8u#,8.88u#),x_height#,desc_depth#);
italcorr x_height#*slant-.5serif_fit#-.3u#;
adjust_fit(0,.5serif_fit#);
numeric arm_thickness[],z_stem,stem[];
stem1=fudged.stem-4stem_corr;
if hefty: arm_thickness1=stem1; arm_thickness2=stem1;
 z_stem=fudged.hair;
else: arm_thickness1=stem1; arm_thickness2=stem1; z_stem=fudged.hair; fi
if arm_thickness1<tiny.breadth: arm_thickness1:=tiny.breadth; fi
pickup tiny.nib; lft x1r=lft x2r=hround(.8u); 
rt x3l=rt x4l=hround.7[.8u, w-.5u]; 
top y1=h; y2=min(y1,h-2/3arm_thickness1);
bot y3=.3[0,h];
numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x3l-x2r-slant*(y2-y3));
penpos1(alpha*(z_stem-tiny),-180); penpos2(alpha*(z_stem-tiny),-180);
penpos3(alpha*(z_stem-tiny),-180); penpos4(alpha*(z_stem-tiny),-180);
filldraw stroke z1e--z2e--z3e;
pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,0);
top y5r=h; x5=x1; rt x6r=w-hround .75u; y6=good.y(y5l-beak/1.8)-eps;
arm(5,6,a,beak_darkness,.4beak_jut);  % upper arm and beak
pickup fine.nib;
pos12(stem,angle(z2-z3)); z12l=z3r;
pos7(curve,180); pos8(vair,270);
lft x7r=hround(.75u); y7=.5[bot y12l,top y8l];
x8=w-hround .5[.5u, w-.8u]; bot y8r=-d-oo;
filldraw stroke pulled_arc.e(12,7) & pulled_arc.e(7,8);  % bowl
if serifs: numeric bulb_diam[];
 bulb_diam2=flare; %+cap_stem-stem;
 pos10(bulb_diam2,0); pos9(cap_hair,0);
 rt x10r=w-hround .75u; y10=-.35d;
 bulb(8,9,10);  % lower bulb
else: pos9(.6[vair',flare],-80);
 rt x9r=w-hround .75u; top y9l=vround -.3d;
 y9r:=good.y y9r-eps; x9l:=good.x x9l;
 filldraw stroke term.e(8,9,right,1,3); fi  % lower terminal
penlabels(1,2,3,4,5,6,7,8,9,10,12); endchar;

cmchar "Soft Sign";
beginchar(oct"272",CT(8.5u#+serif_fit#,8.88u#),x_height#,0);
italcorr .5x_height#*slant-1u#;
adjust_fit(serif_fit#,0);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
lft x1l=lft x2l=hround(side_gap-.5stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
pos3(vair,90); pos5(curve,0); pos7(vair,-90);
top y3r=vround(.6x_height+.5vair); y5=.45[y7,y3];
bot y7r=bot y2; rt x5r=hround(w-1.5u+.5curve); x3=x7=x1;
filldraw stroke z3e{right}...z5e...{left}z7e;  % lobe
if serifs: nodish_serif(1,2,a,1/3,jut,b,1/3,jut);  % upper serif
 nodish_serif(2,1,c,1/3,jut,d,1/3,0jut); fi  % lower serif
penlabels(1,2,3,5,6,7); endchar;

cmchar "Hard Sign";
beginchar(oct"273",10u#+serif_fit#,x_height#,0);
italcorr .5x_height#*slant-1u#;
adjust_fit(serif_fit#,0);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
lft x1l=lft x2l=hround(4u-.5stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
pos3(vair,90); pos5(curve,0); pos7(vair,-90);
top y3r=vround(.6x_height+.5vair); y5=.45[y7,y3];
bot y7r=bot y2; rt x5r=hround(w-1.5u+.5curve); x3=x7=x1;
filldraw stroke z3e{right}...z5e...{left}z7e;  % lobe
if serifs: nodish_serif(1,2,a,1/3,jut,b,1/3,jut);  % upper serif
 nodish_serif(2,1,c,1/3,jut,d,1/3,0jut); fi  % lower serif
pickup crisp.nib; pos8(slab,90);pos9(hair,180);
top y8r = h; x8 = x1;
lft x9r = hround .75u; y9 = good.y(y8l-beak/1.8) - eps;
arm(8,9,e,beak_darkness, -.4beak_jut);
penlabels(1,2,3,5,6,7,8,9); endchar;

cmchar "Raised Glottal Stop";
beginchar(oct"274",CT(8u#,8.88u#),body_height#,0);
italcorr .8asc_height#*slant;
adjust_fit(0,0); pickup tiny.nib;
pos2(vair,90); pos3(curve,0);
pos4(vair,-90); pos5(stem,0); pos6(stem,0);
lft x6l=hround(.47w-.5stem);
x2=x5=x6; x4=x5l;
rt x3r=hround(w-u); bot y6=.8x_height;
top y2r=h+oo; y3=.5[y2,y4]; y4r=.25[y6,y2]; y5=y4l;
{{interim superness:=more_super;
 filldraw stroke pulled_super_arc.e(2,3)(superpull)
  & z3e{down}...{(-10,-1)}z4e;
 filldraw stroke z5e--z6e\\}};  % arc and stem
if serifs: pos1(hair,180); pos0(flare,180);
 lft x1r=hround u; y1=.3[y3,y2]; bulb(2,1,0);  % bulb
else: pickup fine.nib; pos2'(vair,90); z2'=z2;
 pos1(vround 5/7[vair,flare],110);
 lft x1r=hround u; top y1r=vround .9[y6,top y2r];
 filldraw stroke term.e(2',1,left,1,4); fi  % terminal
penlabels(0,1,2,3,4,5,6); endchar;

% end of tipasym3.mf
