% tipasym2.mf: TIPA phonetic symbols (2)
% Copyright 1996-2003 FUKUI Rei
%
% This program may be distributed and/or modified under the
% conditions of the LaTeX Project Public License, either version 1.2
% of this license or (at your option) any later version.
% The latest version of this license is in
%   http://www.latex-project.org/lppl.txt
% and version 1.2 or later is part of all distributions of LaTeX 
% version 1999/12/01 or later.
%
% This program consists of all files listed in Manifest.txt.
%
% Version 1.2 2003/01/01
%
% This font is based on:
%   Computer Modern font series by D. E. Knuth and
%   TSIPA by KOBAYASHI Hajime, FUKUI Rei and SHIRAKAWA Shun.
%

% symbols in the section '340--'377

cmchar "Hooktop B";
beginchar(oct"341",10u#,asc_height#,0);
italcorr .7[x_height#,asc_height#]*slant+min(.5curve#-u#,-.25u#);
adjust_fit(if serifs:-.5u# else:0 fi,0);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0l=z0'l; x0'=x1; x0=x2;
lft x1l=hround(side_gap-.5stem'); top y1=.1[x_height,h];
numeric edge; edge=rt x2r;
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,180);
pos4(vair,90); pos5(curve,0); pos6(vair,-90); penpos7(x3l-x3r,-180);
rt x3l=max(rt x3l-(lft x3r-tiny.lft x2l),1/3[rt x2,edge]);
y3=1/8[bar_height,x_height];
x4l=w-.5(w-serif_fit)+.5u; top y4r=x_height+oo;
rt x5r=hround min(w-1.35u+.5curve,w-.6u); y5=.5x_height;
x6l=x4l-.2u; bot y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=min(x,.5[x4,x5r]);
(x',y6r)=whatever[z7l,z6l]; x6r:=min(x',.5[x6,x5r]);
filldraw stroke z3e{up}...pulled_arc.e(4,5)&pulled_arc.e(5,6)...{up}z7e; % bowl
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{right}z4l));
y2=ypart(((edge,h)--(edge,0))intersectionpoint(z6l{left}...{up}z7l));
pickup tiny.nib; filldraw stroke z1e--z0'e--z0e--z2e;  % stem
pickup crisp.nib; pos8(hair,0); pos7'(stem,0);
z7'=z2; x8l=x7'l; bot y8=0;
filldraw stroke z7'e--z8e;  % point
hooktop(1,9,10,11,stem',hround(w-1u),h,.8,.55,.2);
penlabels(0,1,2,3,4,5,6,7,8,9,10,11); endchar;

cmchar "Hooktop D";
beginchar(oct"342",10u#+serif_fit#,asc_height#,0);
italcorr asc_height#*slant+1.75u#;
adjust_fit(0,serif_fit#);
d_stroke(false,true,0);
hooktop(0,9,10,11,stem,hround(w+1.5u),h,.9,.6,.5);
penlabels(0,1,2,3,4,5,6,7,9,10,11); endchar;

cmchar "Right-tail D";
beginchar(oct"343",10u#,asc_height#,desc_depth#);
italcorr asc_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(0,serif_fit#);
d_stroke(true,false,0);
if serifs: right_tail(2,8,9,10,stem,hround(w+1u),.88,.5,1/3);
else: right_tail(2,8,9,10,stem,hround(w+1u),.833,.5,1/3); fi
penlabels(0,1,2',3,4,5,6,7,8,9,10); endchar;

cmchar "Hooktop G";
beginchar(oct"344",10u#+serif_fit#,.9asc_height#,desc_depth#);
h:=vround(.9asc_height#*hppp); d:=vround(desc_depth#*hppp); % vertical adj.
italcorr .9asc_height#*slant+1.75u#;
adjust_fit(0,0);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0r=z0'r; x0'=x1; x0=x2;
rt x1r=hround(w-side_gap+.5stem');
numeric edge; edge=lft x2l;
path edge_path; edge_path=(edge,h)--(edge,0);
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,0);
pos4(vair,90); pos5(curve,180);
pos6(vair,270); penpos7(x3r-x3l,360);
lft x3l=2/3[lft x2,edge]; y3=bar_height;
x4l=.5(w-serif_fit)-.3u; top y4r=x_height+oo;
lft x5r=hround max(1.35u-.5curve,.6u); y5=.5x_height;
x6l=x4l-.2u; bot y6r=vround 1/3vair;
lft x7l=edge; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=max(x',.5[x5r,x6]);
filldraw stroke z3e{up}...{left}z4e&super_arc.e(4,5)
 &super_arc.e(5,6)&z6e{right}...{up}z7e;  % bowl
y1=ypart(edge_path intersectionpoint(z3l{up}...{left}z4l));
y0=ypart(edge_path intersectionpoint(z7l{down}...{left}z6l));
pickup tiny.nib; bot y2=if serifs: -.25d else: 0 fi;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: pickup tiny.nib;
 pos9(vair,-90); x9=.5[x2,x10]; bot y9r=-d-o-1;
 pos10(hair,-180); lft x10r=hround u; y10=-.75d+.5flare;
 pos11(flare,-180); z11r=z10r;
 bulb(9,10,11); filldraw stroke super_arc.e(2,9);  % tail
else: pickup fine.nib; pos2'(stem,0); z2'=z2;
 z2''r=z2'r; z2''=z2'; z2''l=(x2'l,0);
 pos9(vair,-90); x9=4.5u; bot y9r=-d-o-1;
 pos10(.5[vair,flare],-90); lft x10=hround 1.25u;
 y10r=good.y -5/6d; y10l:=good.y y10l;
 filldraw stroke z2'e..z2''e&super_arc.e(2'',9)
  & term.e(9,10,left,.9,4); fi  % tail
hooktop(0,12,13,14,stem',hround(w+1.5u),h,.92,.55,.7);
penlabels(0,1,2,3,4,5,6,7,9,10,11,12,13,14); endchar;

cmchar "Crossed H";
beginchar(oct"350",10u#,asc_height#,0);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(stem_shift#+.5u#,serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
pos1'(stem',0); pos2'(stem',0); pos3(stem,0);
lft x1l=hround(side_gap-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=h; bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % arch and right stem
if serifs: sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); % lower left serif
 dish_serif(4,3,e,1/3,inner_jut,f,1/3,jut); fi % lower right serif
hbar(5,6,lft x1l - hround(2u),rt x1r + hround(side_gap),
 x_height + .5(h - x_height));
penlabels(1,2,3,4); endchar;

cmchar "Barred dotless J";
beginchar(oct"351",CT(5.5u#,5.56u#),x_height#,desc_depth#);
italcorr x_height#*slant-serif_fit#+.5stem#-.5u#;
adjust_fit(2stem_shift# if monospace:+1u# fi,
 -2stem_shift# if monospace:+.5u# fi);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
rt x1r=hround(.5w+.25u+.5stem'); x1=x2;
top y1=x_height; bot y2=-1/3d;
filldraw stroke z1e--z2e;  % stem
if serifs: dish_serif(1,2,b,1/3,1.25jut,c,1/3,jut); % serif
 pickup tiny.nib; pos5(vair,-90); pos6(hair,-180); pos7(flare,-180);
 x5=.5[x2,x6r]; bot y5r=-d-oo; y6-.5flare=-.88d;
 if monospace: lft x6r=0 else: z6r=z7r; rt x7l=floor .75u fi;
 (x,y5r)=whatever[z5l,z2l]; x5r:=max(x,.5[x6r,x5]);
 filldraw stroke z2e{down}...z5e{left};  bulb(5,6,7); % arc and bulb
else: pickup fine.nib; pos2'(stem',0); z2'=z2;
 pos6(.2[vair,stem'],-90); pos7(vair,-90);
 lft x7r=hround -.75u; bot y7r=vround 5/6(-d-oo);
 x3r=x1r; top y4r=min(2x_height,h+1);
 (x,y7l)=whatever[z7r,(x1,min(h,asc_height))]; x7l:=x;
 z5r=z2'r; (x2'l,y5l)=whatever[z7l,z5r]; x5l=x2'l; y5=y5r;
 x6r=.5[x7r,x5r]; x6l:=.5[x7l,x5l]; bot y6r=-d-oo;
 filldraw stroke z2'e..{down}z5e & super_arc.e(5,6)
  & term.e(6,7,left,1,4); fi  % arc and terminal
hbar(8,9,lft x1l - hround(1.75u),rt x1r + hround(1.5u),.58x_height);
penlabels(1,2,5,6,7); endchar;

cmchar "Hooktop barred dotless J";
beginchar(oct"352",6u#,.9asc_height#,desc_depth#);
italcorr h#*slant+1u#;
adjust_fit(if monospace: 1u#,1u# else: 0,0 fi);
pickup if serifs: tiny.nib; else: fine.nib; fi
pos1(stem',0); pos2(stem',0); x1=x2=.5w;
top y1=x_height if serifs: +min(oo,serif_drop) fi; bot y2=0;
filldraw stroke z1e--z2e;  % stem
left_tail(2,5,6,7,stem',hround-.75u);
hooktop(1,8,9,10,stem',hround(w+.75u),h,.84,.5,.4);
pickup crisp.nib; top y3r=top y4r=vround(bar_height+.5bar);
lft x3=x2-2.5u; x4=w-x3;
pos3(bar,90); pos4(bar,90);
filldraw stroke z3e--z4e;  % bar
penlabels(1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "L with tilde";
beginchar(oct"353",7u#,asc_height#,0);
l_width#:=5u#+2serif_fit#;
numeric hook_depth#; hook_depth#:=1.1desc_depth#;
define_whole_vertical_pixels(hook_depth);
italcorr asc_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#+.3u#,serif_fit#-stem_shift#+.3u#);
numeric tilde_height; tilde_height:=min(.66h,.9x_height); % tilde
if serifs: numeric theta; theta=angle(1/6(6u-vair),1/4(h-x_height));
 pickup crisp.nib; numeric mid_width; mid_width=.4[vair,stem];
 pos1(vair,theta+90); pos2(vair,theta+90);
 pos3(vair,theta+90); pos4(vair,theta+90);
 z2-z1=z4-z3=(mid_width-crisp)*dir theta;
 lft x1r=w-rt x4l=hround .75u; top y4r=tilde_height;
 bot y1l= top y4r - .13h;
 pair delta; ypart delta=3(y3l-y1l); delta=whatever*dir theta;
 filldraw z1l..controls(z1l+delta)and(z3l-delta)..z3l..z4l
  --z4r..controls(z4r-delta)and(z2r+delta)..z2r..z1r--cycle;  % stroke
else: pickup fine.nib; pos1(vair,180); pos2(vair,90);
 pos3(.5[vair,slab],90); pos4(vair,90); pos5(vair,180);
 lft x1r=w-rt x5l=hround .75u; x2-x1=x3-x2=x4-x3=x5-x4;
 bot y1=bot y4l=top y5 - .13h;
 top y2r=top y5=vround(tilde_height); y3=.5[y2,y4];
 filldraw stroke z1e{up}...z2e{right}..z3e..{right}z4e...{up}z5e; fi % stroke
penlabels(1,2,3,4,5);
pickup tiny.nib; pos6(stem',0); pos7(stem',0);
if odd(w-stem'): change_width; fi
lft x6l=hround(.5w-.5stem'); x6=x7; top y6=h; bot y7=0;
filldraw stroke z6e--z7e;  % stem
if serifs: sloped_serif.l(6,7,a,1/3,jut,serif_drop);  % upper serif
 dish_serif(7,6,b,1/3,jut,c,1/3,jut); fi  % lower serif
penlabels(6,7); endchar;

cmchar "Belted L";
beginchar(oct"354",7u#,asc_height#,0);
l_width#:=5u#+2serif_fit#;
numeric hook_depth#; hook_depth#:=1.1desc_depth#;
define_whole_vertical_pixels(hook_depth);
italcorr asc_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift# if monospace:+1u# fi,
 if monospace: 1u#+fi serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.5w-.5stem'); x1=x2; top y1=h; bot y2=0;
filldraw stroke z1e--z2e;  % stem
if serifs: sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % upper serif
 dish_serif(2,1,b,1/3,jut,c,1/3,jut); fi  % lower serif
if serifs: pickup crisp.nib;
 pos13(vair,90); pos14(max(.8stem,vair),180); pos15(vair,-50);
 x13=x1; x14r=x1-3.2u; x15=x1+2.8u;
 top y13r=x_height; y14=y15; bot y15=if monospace:.65 else:.8 fi x_height;
 filldraw stroke z13e{(-10,1)}...z14e{down}...{(1,1)}z15e;  % hook
else: pickup fine.nib;
 pos13(vair,85); pos14(min(.8stem,vair),180); pos15(vair,-50);
 x13=x1; x14r=x1-2.8u; x15=x1+2.6u;
 top y13r=x_height; y14=y15; bot y15=.7x_height;
 filldraw stroke z13e{(-10,1)}...z14e{down}...{(1,1)}z15e; fi % hook
penlabels(1,2,11,12,13,14,15); endchar;

cmchar "Right-tail L";
beginchar(oct"355",CT(5u#,5.56u#),asc_height#,desc_depth#);
italcorr asc_height#*slant+.5stem#-1.5u#;
adjust_fit(serif_fit#+stem_shift# if monospace:+.5u# fi,
 if monospace: 1.5u#fi -stem_shift#);
pickup if serifs: tiny.nib; else: fine.nib; fi
pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.5w-.5stem'); x1=x2; top y1=h; bot y2=0;
filldraw stroke z1e--z2e;  % stem
if serifs: sloped_serif.l(1,2,a,1/3,jut,serif_drop);fi% upper serif
if serifs: right_tail(2,3,4,5,stem',hround(w+1u),.88,.5,1/3);
else: right_tail(2,3,4,5,stem',hround(w+1u),.833,.5,1/3); fi
penlabels(1,2,3,4,5); endchar;

cmchar "Turned M, right leg";
beginchar(oct"356",CT(15u#,15.56u#),x_height#,desc_depth#);
italcorr x_height#*slant+serif_fit#;
adjust_fit(serif_fit#-stem_shift#,serif_fit#+stem_shift#);
numeric shaved_stem; shaved_stem=hround(mfudged.stem-2stem_corr);
pickup tiny.nib; pos1(mfudged.stem,0); pos2(mfudged.stem,0);
pos1'(shaved_stem,0); pos2'(shaved_stem,0);
pos3(mfudged.stem,0); pos5(mfudged.stem,0);
rt x1r=hround(w-(2.5u-.5stem)); x1r=x1'r=x2r=x2'r;
rt x3r=hround(.5w+.5stem); x5-x3=x3-x1;
if not monospace: r:=hround(x5+x1)-l; fi  % change width for better fit
bot y1=-d-min(oo,serif_drop); top y2=h; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
turned_h_stroke(2,a,3,4);  % left arch and middle stem
turned_h_stroke(4,b,5,6);  % right arch and right stem
if serifs: sloped_serif.r(1',2',c,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if lft x2l-jut-.5u-1>=rt x4r+jut: inner_jut=jut;
 else: lft x2l-jut-.5u-1=rt x4r+inner_jut; fi
 dish_serif(2,1,d,1/3,jut,e,1/3,jut); % lower left serif
 dish_serif(4,3,f,1/3,inner_jut,g,1/3,jut); % lower middle serif
 dish_serif(6,5,h,1/3,inner_jut,i,1/3,jut); fi % lower right serif
penlabels(1,2,3,4,5,6); endchar;

cmchar "Right-tail N";
beginchar(oct"357",10u#,x_height#,desc_depth#);
italcorr .5[bar_height#,x_height#]*slant+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,-stem_shift#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
lft x1l=hround(side_gap-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=h+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % arch and right stem
if serifs:
 sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
  if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
  else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); fi % lower left serif
if serifs: right_tail(4,5,6,7,stem,hround(w+1u),.88,.5,1/3);
else: right_tail(4,5,6,7,stem,hround(w+1u),.833,.5,1/3); fi
penlabels(1,2,3,4,5,6,7); endchar;

cmchar "Left-tail N (at left)";
beginchar(oct"361",10u#,x_height#,desc_depth#);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0); pos8(stem',0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
lft x1l=hround(side_gap-.5stem); x1l=x1'l=x2l=x2'l=x8l; x3=w-x1;
top y1=h+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2'; bot y8=-1/3d;
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(8,a,3,4);  % arch and right stem
if serifs: sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(4,3,e,1/3,inner_jut,f,1/3,jut); fi % lower right serif
left_tail(2',5,6,7,shaved_stem,hround(-.75u));
penlabels(1,2,3,4,5,6,7); endchar;

cmchar "Bull's eye";
beginchar(oct"362",CT(14u#-width_adj#,14.44u#),cap_height#,0);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
penpos1(vair',90); penpos3(vround(vair+1.5vair_corr),-90);
penpos2(cap_curve,180); penpos4(cap_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=sqrt superness;  % make "O", not "0"
else: x2r=hround u; fi
x4r=w-x2r; x1=x3=.5w; y1r=h+o; y3r=-o;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
fill fullcircle scaled(dot_size+eps) shifted(.5[z3,z1]);  % dot
math_fit(-.3cap_height#*slant-.5u#,ic#-.5u#); penlabels(1,2,3,4); endchar;

cmchar "Right-tail R";
numeric r_flare#; r_flare#=.75[if serifs: stem# else: vair# fi,flare#];
define_whole_blacker_pixels(r_flare);
beginchar(oct"363",CT(if serifs:max(7u#,5.5u#+r_flare#) else:6.5u# fi,6.66u#),
 x_height#,desc_depth#);
italcorr x_height#*slant if not serifs: +.25u# fi;
adjust_fit(serif_fit#,0);
pickup fine.nib; top y4r=h+oo;
if serifs: pos4(vair,90); pos5(hair,0);
 x4=w-max(if Times_Compat: 1.6 else: 1.75 fi u,.25u+r_flare);
 rt x5r=hround(w-if Times_Compat: .1u else: .5u fi +.5);
 y5+.5r_flare=.9[bar_height,h]+oo;
 pos6(r_flare,0); bulb(4,5,6);  % bulb
else: pos4(r_flare,90); rt x4=hround(w-.25u); fi
pos3(thin_join,180); rt x3l=hround(side_gap-.5stem')+stem'; top y3=bar_height;
filldraw stroke z3e{up}...{right}z4e;  % arc
pickup tiny.nib; pos0(stem',0); pos2(stem',0);
pos1(hround(stem-3stem_corr),0); top y1=h+min(oo,serif_drop);
pos0'(hround(stem-3stem_corr),0); y0=y0'=y3; x1l=x0l=x0'l=x2l;
lft x1l=hround(side_gap-.5stem'); bot y2=0;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: sloped_serif.l(1,0',a,1/3,jut,serif_drop); % upper serif
 pickup tiny.nib;
 right_tail(2,7,8,9,stem',hround(w-.1u),.88,.5,1/3);
else: pickup fine.nib;
 right_tail(2,7,8,9,stem',hround(w-.1u),.833,.5,1/3); fi
penlabels(1,2,3,4,5,6,7,8,9); endchar;

cmchar "Turned R";
numeric r_flare#; r_flare#=.75[if serifs: stem# else: vair# fi,flare#];
define_whole_blacker_pixels(r_flare);
beginchar(oct"364",CT(if serifs:max(7u#,5.5u#+r_flare#) else:6.5u# fi,6.66u#),
 x_height#,0);
italcorr x_height#*slant if not serifs: -.5u# fi;
adjust_fit(0,serif_fit#);
pickup fine.nib; bot y4r=-oo;
if serifs: pos4(vair,-90); pos5(hair,-180);
 x4=max(if Times_Compat: 1.6 else: 1.75 fi u,.25u+r_flare);
 lft x5r=hround(if Times_Compat: .1u else: .5u fi -.5);
 y5-.5r_flare=h-.9[bar_height,h]-oo;
 pos6(r_flare,-180); bulb(4,5,6);  % bulb
else: pos4(r_flare,-90); lft x4=hround(.25u); fi
pos3(thin_join,0); lft x3l=hround(w-side_gap+.5stem')-stem'; bot y3=h-bar_height;
filldraw stroke z3e{down}...{left}z4e;  % arc
pickup tiny.nib; pos0(stem',0); pos2(stem',0);
pos1(hround(stem-3stem_corr),0); top y2=h;
pos0'(hround(stem-3stem_corr),0); y0=y0'=y3; x1r=x0r=x0'r=x2r;
rt x1r=hround(w-side_gap+.5stem'); bot y1=-min(oo,serif_drop);
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: sloped_serif.r(1,0',a,1/3,jut,serif_drop);  % lower serif
 dish_serif(2,0,b,1/3,1.25jut,c,1/3,jut); fi  % upper serif
penlabels(1,2,3,4,5,6); endchar;

cmchar "Turned R, right tail";
numeric r_flare#; r_flare#=.75[if serifs: stem# else: vair# fi,flare#];
define_whole_blacker_pixels(r_flare);
beginchar(oct"365",CT(if serifs:max(7u#,5.5u#+r_flare#) else:6.5u# fi,6.66u#),
 x_height#,desc_depth#);
italcorr x_height#*slant if not serifs: -.5u# fi;
adjust_fit(0,serif_fit#);
pickup fine.nib; bot y4r=-oo;
if serifs: pos4(vair,-90); pos5(hair,-180);
 x4=max(if Times_Compat: 1.6 else: 1.75 fi u,.25u+r_flare);
 lft x5r=hround(if Times_Compat: .1u else: .5u fi -.5);
 y5-.5r_flare=h-.9[bar_height,h]-oo;
 pos6(r_flare,-180); bulb(4,5,6);  % bulb
else: pos4(r_flare,-90); lft x4=hround(.25u); fi
pos3(thin_join,0); lft x3l=hround(w-side_gap+.5stem')-stem'; bot y3=h-bar_height;
filldraw stroke z3e{down}...{left}z4e;  % arc
pickup tiny.nib; pos0(stem',0); pos2(stem',0);
pos1(hround(stem-3stem_corr),0); top y2=h;
pos0'(hround(stem-3stem_corr),0); y0=y0'=y3; x1r=x0r=x0'r=x2r;
rt x1r=hround(w-side_gap+.5stem'); bot y1=-min(oo,serif_drop);
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: dish_serif(2,0,b,1/3,1.25jut,c,1/3,jut);  % upper serif
 right_tail(2,7,8,9,hround(stem'),hround(w+1u),.88,.5,1/3);
else: pickup fine.nib;
 right_tail(2,7,8,9,hround(stem'),hround(w+1u),.833,.5,1/3); fi
penlabels(1,2,3,4,5,6,7,8,9); endchar;

cmchar "Right-tail S (at left)";
beginchar(oct"371",CT(7.1u#,7.78u#),x_height#,desc_depth#);
italcorr x_height#*slant-if serifs:.55u# else:.3u# fi;
adjust_fit(if monospace: .5u#,.4u# else: 0,if serifs:0 else:-.2u# fi fi);
numeric theta; theta=90-angle(40u,h); slope:=-h/40u;  % angle at middle
numeric s_slab; s_slab=if serifs:vair else:Vround .1[vair,stem] fi;
numeric ess'; ess'=max(fine.breadth,ess);
pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-100);
pos0(ess',theta); pos7(s_slab,-90); x2l=x0=x7=.5w;
top y2l=h+vround 1.5oo; bot y7r=-oo;
y0-.5ess'=y7l+if serifs:.54 else: .52 fi\\(y2r-y7l-ess');
lft x3l=hround .6u; rt x6r=hround(w-.6u);
x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
interim superness:=more_super;
filldraw stroke super_arc.e(2,3) & z3e{down}
 ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
if serifs: pos1(hair,180); pos8(hair,180);
 rt x1l=hround(w-1.05u); lft x8r=hround .6u;
 bot y1=min(bot y2r,vround 1/5[top y5r,h]);
 top y8=max(top y7l,vround 5/6 bot y4l);
 filldraw stroke z1e{up}....{left}z2e;  % upper arc
 filldraw stroke z7e{left}....{x8-x7,5(y8-y7)}z8e;  % lower arc
 path upper_arc, lower_arc;
 upper_arc=z1{up}....{left}z2; lower_arc=z7{left}....{x8-x7,5(y8-y7)}z8;
 pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
 x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
 x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
 numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
 filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle;  % upper barb
 t:=xpart(lower_arc intersectiontimes(z9r--z8'));
 filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle;  % lower barb
else: pos1(4/7[s_slab,flare],-100); pos8(flare,-90);
 x1l=good.x(x1l+w-u-rt x1); lft x8r=hround .5u;
 top y1l=vround(.93h+1.5oo); bot y8r=vround .1h-oo;
 filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
 filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
if serifs: pos11(hair,0); y11=y9l; x11l=x8r;
 right_tail(11,12,13,14,hair,hround(x7+.5u),.88,.5,1/3);
else: pos11(.8stem,-10); y11=y8r; lft x11l=lft x8l;
 right_tail(11,12,13,14,.8stem,hround(x7+.5u+.25hair),.833,.5,1/3); fi
penlabels(0,1,1',2,3,4,5,6,7,8,8',9,10,11,12,13,14);
endchar;

cmchar "Right-tail T";
beginchar(oct"372",6u#+max(u#,.5stem#),
 min(asc_height#,if hefty:9/7 else:10/7 fi\\ x_height#),desc_depth#);
italcorr x_height#*slant if serifs: -.9u# else: -.4u# fi;
adjust_fit(0,if serifs: -.5u# else: -.5u# fi);
numeric shaved_stem; shaved_stem=hround(stem if hefty:-\\2stem_corr fi);
pickup fine.nib; pos2(shaved_stem,180); pos3(shaved_stem,180);
lft x2r=lft x3r=hround(side_gap-.5shaved_stem); y2=y8; y3=0;
pickup crisp.nib; pos8(bar,90);
rt x8=hround(.93w- if Times_Compat: .4u else: 1.3u fi);
top y8r=x_height; lft x7=hround 1/3u; y7l=y8l;
if hefty: pos7(bar,90);
 filldraw stroke z7e--z8e;  % crossbar
 pickup tiny.nib; pos1(hround(shaved_stem-stem_corr),0);
 rt x1r=fine.rt x2l; top y1=h;
 penpos2'(x1r-x1l,0); x2'=x1; y2'=y2;
 filldraw stroke z1e--z2'e;  %terminal
else: pos7(vair,90); pos1(hair,0);
 rt x1r=fine.rt x2l; top y1=h;
 filldraw z1l{down}...{left}z7r--z7l--z8l
  --z8r--(x1r,y8r)--z1r--cycle; fi  % upper terminal and crossbar
pickup fine.nib;
filldraw stroke z2e--z3e;  % stem
if serifs: right_tail(3,9,10,11,shaved_stem,hround(w-.5u),.88,.5,1/3);
else: right_tail(3,9,10,11,shaved_stem,hround(w-1u),.833,.5,1/3); fi
penlabels(1,2,3,7,8,9,10,11); endchar;

cmchar "Turned W";
beginchar(oct"373",CT(13u#,14.44u#),x_height#,0);
adjust_fit(serif_fit#,serif_fit#);
numeric stem[],outer_jut,upper_notch,alpha,mid_corr;
outer_jut=.7jut; x1l=w-x8r=l+letter_fit+outer_jut+.25u;
stem4=fudged.stem-stem_corr;
stem1=min(fudged.hair if hefty:-2stem_corr fi,stem4);
stem3=if hefty:.1[vair,stem1] else: stem1 fi;
stem2=max(stem3,stem4-3stem_corr);
x2-x1=x4-x3=x6-x5=x8-x7; x2r+apex_corr=x3r; x6r+apex_corr=x7r;
y1=y8=0; y2=y3=y6=y7=h+apex_oo;
y4=y5=if monospace: vround .4h else: 0 fi; upper_notch=y4+notch_cut;
mid_corr=if monospace or hefty:-.3apex_corr else: .5jut fi;
alpha=diag_ratio(4,stem3-stem2+stem1,y2-y1,x8r-x1l+mid_corr-2apex_corr);
penpos1(alpha*stem1,0); penpos2(alpha*stem1,0);
penpos3(alpha*stem2,0); penpos4(alpha*stem2,0);
penpos5(alpha*stem3,0); penpos6(alpha*stem3,0);
penpos7(alpha*stem4,0); penpos8(alpha*stem4,0);
 x8r-x5r=floor(x8r-x5r+.5(x5l+mid_corr-x4l)); % x5r $\approx$ x4r+mid_corr
z23=whatever[z1r,z2r]=whatever[z3l,z4l];
z45=whatever[z3r,z4r]=whatever[z5l,z6l];
z67=whatever[z5r,z6r]=whatever[z7l,z8l];
fill diag_end(1l,2l,1,1,3r,4r){z4-z3}
 ...if y45>upper_notch:(x45,upper_notch)+.5left{down}
   --(x45,upper_notch)+.5right{up} else: z45&z45 fi
 ...{z6-z5}diag_end(5l,6l,1,1,7r,8r)--diag_end(7r,8r,1,1,8l,7l){z7-z8}
 ...if y67<h-notch_cut:(x67,h-notch_cut)+.5right{up}
   --(x67,h-notch_cut)+.5left{down} else: z67&z67 fi
 ...{z5-z6}turned_special_diag_end(6,5,4,3){z3-z4}
 ...if y23<h-notch_cut:(x23,h-notch_cut)+.5right{up}
   --(x23,h-notch_cut)+.5left{down} else: z23&z23 fi
 ...{z1-z2}diag_end(2r,1r,1,1,1l,2l)--cycle;  % diagonals
if serifs: numeric inner_jut[]; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3); prime_points_inside(8,7);
 if monospace: inner_jut1=inner_jut4=1.5jut;
 elseif hefty: inner_jut1=inner_jut4=jut;
 else: fill diag_end(3l,4l,1,1,4r,3r)--.9[z4r,z3r]
   ..{z4-z3}.1[z4l,z3l]--cycle; % middle stem
  inner_jut2=.7inner_jut1; inner_jut4=1.1inner_jut3;
  if lft x8'l-jut-.5u-1>=rt x4'r+.7jut: inner_jut1=jut;
  else: lft x8'l-inner_jut1-.5u-1=rt x4'r+inner_jut2; fi
  if lft x4'l-jut-.5u-1>=rt x1'r+1.1jut: inner_jut3=jut;
  else: lft x4'l-inner_jut3-.5u-1=rt x1'r+inner_jut4; fi
  dish_serif(4',3,e,1/2,inner_jut3,f,1/3,inner_jut2); fi  % middle serif
 dish_serif(1',2,a,1/2,outer_jut,b,.6,inner_jut4)(dark);  % left serif
 dish_serif(8',7,c,1/2,inner_jut1,d,1/3,outer_jut); fi  % right serif
penlabels(0,1,2,3,4,5,6,7,8,23,45,67); endchar;

cmchar "Right-tail Z";
beginchar(oct"374",CT(8u#,8.88u#),x_height#,desc_depth#);
italcorr x_height#*slant-.5serif_fit#-.3u#;
adjust_fit(0,if monospace:.5u#+fi .5serif_fit#);
numeric arm_thickness[],z_stem;
if hefty: arm_thickness1=Vround(vair-vair_corr); arm_thickness2=vair;
 z_stem=.6[vair,stem];
else: arm_thickness1=vair; arm_thickness2=vair'; z_stem=.9[vair,stem]; fi
pickup tiny.nib; rt x1r=rt x2r=hround(w-.8u); lft x3l=lft x4l=hround .5u;
top y1=h; y2=min(y1,h-2/3arm_thickness1);
bot y4=0; y3=max(y4,2/3arm_thickness2);
numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l-slant*(y2-y3));
penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
pair delta; delta=penoffset z3-z2 of currentpen;
fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
 ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
 ---cycle;  % diagonal
pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
top y5r=h; x5=x1; lft x6r=hround .75u; y6=good.y(y5l-beak/1.4)-eps;
arm(5,6,a,beak_darkness,-.4beak_jut);  % upper arm and beak
pos7(arm_thickness2,-90); pos8(hair,0);
bot y7r=0; x7=x4; rt x8r=hround(w-.75u); y8=good.y(y7l+beak/1.2)+eps;
arm(7,8,b,beak_darkness,.6beak_jut);  % lower arm and beak
if serifs: pos11(hair,0); y11=y.b0;
 x11r=hround(x.b0-.05u); 
 right_tail(11,12,13,14,hair,hround(w+2u),.88,.5,1/3);
else: pos11(.8stem,0); y11=.5[0,y8]; rt x11r=rt x8r;
 right_tail(11,12,13,14,.8stem,hround(w+1.5u),.833,.5,1/3); fi
penlabels(1,2,3,4,5,6,7,8,11,12,13,14);
endchar;

cmchar "Curly-tail Z";
beginchar(oct"375",CT(9u#,9.88u#),x_height#,desc_depth#);
italcorr x_height#*slant-.5serif_fit#-.3u#;
adjust_fit(0,.5serif_fit#);
numeric arm_thickness[],z_stem;
arm_thickness1=vair; arm_thickness2=1.2vair; z_stem=.9[vair,stem];
pickup tiny.nib; rt x1r=rt x2r=hround(.89w-.8u);
lft x3l=lft x4l=hround .5u;
top y1=h; y2=min(y1,h-2/3arm_thickness1);
bot y4=0; y3=max(y4,2/3arm_thickness2);
numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l-slant*(y2-y3));
penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
pair delta; delta=penoffset z3-z2 of currentpen;
fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
 ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
 ---cycle;  % diagonal
pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
top y5r=h; x5=x1; lft x6r=hround .75u; y6=good.y(y5l-beak/1.4)-eps;
arm(5,6,a,beak_darkness,-.4beak_jut);  % upper arm and beak
pos7(arm_thickness2,90); pos8(arm_thickness2,90);
x7=x4; bot y8l=bot y7l=0; pos9(vair,-80);
if serifs: pos11(hair,180); pos10(hair,0);
else: pos11(fudged.hair,180); pos10(fudged.hair,0); fi
rt x11l=hround(w-1u+.5hair); y11=.5[y9r,y8r];
x9-.5u=x8=.7[x7,x11]; y9=.32h+.5vair; x10l=1.5u+.6hair; bot y10=-.75d;
filldraw stroke z7e--z8e{right}...{up}z11e...{(-5,-1)}z9e
 ...{(-1,-2)}z10e;
penlabels(1,2,3,4,5,6,7,8,8',9,10,11); endchar;

cmchar "Ash; Lower-case A-E ligature";
beginchar(oct"346",13u#,x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(0,0);
numeric left_curve,right_curve;
if monospace: right_curve=left_curve=fudged.stem;
else: left_curve=max(tiny.breadth,hround(curve-2stem_corr));
 right_curve=max(tiny.breadth,hround(curve-if serifs:6 else:8 fi\\stem_corr));fi
pickup tiny.nib; pos11(right_curve,0);
pos12(vair,90); pos13(mfudged.stem,180);
y11=good.y bar_height; top y12r=h+vround 1.5oo; y10l=bot y11;
rt x11r=hround min(w-.5u,w-u+.5right_curve);
lft x13r=hround (.5w-.5mfudged.stem); x12=.55[x13,x11];
{{interim superness:=more_super;
 filldraw stroke super_arc.e(11,12)}};  % right bowl of e
y13=.5[y12,y14]; bot y14r=-oo; x14=x12+.25u;
if serifs: pos14(vair',270); pos15(mfudged.hair,360);
 y15=max(good.y(.5bar_height-.9),y14l+vair); x15r=x11r;
 (x,y14l)=whatever[z14r,z15]; x14l:=min(x,x14l+.5u);
 filldraw stroke pulled_arc.e(12,13)
  & pulled_super_arc.e(13,14)(.8superpull)
  ...{x15-x14,5(y15-y14)}z15e; % left bowl, arc, and terminal of e
else: pos14(vair,270);
 filldraw stroke super_arc.e(12,13)
  & super_arc.e(13,14);  % left bowl and arc of e
 pickup fine.nib; pos14'(vair,270); z14=z14';
 pos15(.5[vair,flare],275); rt x15r=hround(w-.6u);
 y15r=good.y(y15r+1/3bar_height-y15); y15l:=good.y y15l; x15l:=good.x x15l;
 filldraw stroke term.e(14',15,right,1,4); fi  % right terminal
y11'r=y10r=y10l+.6[thin_join,vair]; y11'l=y10l; x11'l=x11'r=x11; x10l=x10r=x13;
fill stroke z10e--z11'e;  % crossbar
pickup fine.nib; top y3r=h+vround 1.5oo;
if serifs: pos1(flare,180); pos2(mfudged.hair,180);
 pos3(vair,90); lft x1r=hround max(u,2.1u-.5flare); x3=4u;
 y1=min(bar_height+.5flare+2vair+2,.9[bar_height,h]-.5flare);
 bulb(3,2,1);  % bulb
else: pos1(5/7[vair,flare],95); x1l=good.x 1.5u; x1r:=good.x x1r;
 pos3(1/8[vair,thin_join],90);
 x3=4.3u; top y1r=vround .82[bar_height,top y3r];
 filldraw stroke term.e(3,1,left,.9,4); fi  % left terminal
pos4(mfudged.stem,0); x4=x13; y4=1/3[bar_height,h];
pos5(mfudged.stem,0); x5=x4; y5=min(y4,y13);
filldraw stroke super_arc.e(3,4)&z4e--z5e;  % arc and stem
pos6(.6[thin_join,vair],90); x6=x4; bot y6=y10l;
pos7(left_curve,if (Times_Compat and serifs): 200 else: 180 fi);
lft x7r=hround max(.5u,1.5u-.5left_curve); y7=1/3[top y8l,top y6r];
if (Times_Compat and serifs): pos8(.6[vair,flare],210); else: pos8(vair,270);fi
x8l=3.75u; bot y8r=-oo;
pos9(.5[vair,fudged.stem],360); x9=x5; y9=.55bar_height;
(x',y8r)=whatever[z8l,z9l]; x8r:=max(x',x8-u);
{{interim superness:=more_super;
 filldraw stroke z9e{down}...z8e{left}...{up}z7e&%super_arc.e(7,6)}}; % bowl
 if (Times_Compat and serifs): z7e{up}...{(50,10)}z6e
 else: super_arc.e(7,6)fi}}; % bowl
if y9<y5: filldraw stroke z5e{down}..{down}z9e; fi  % link (usually hidden)
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15); endchar;

cmchar "C cedilla";
beginchar(oct"347",CT(8u#,8.88u#),x_height#,0.875desc_depth#);
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
c_stroke(w); x6=x4;
if serifs: pickup crisp.nib; pos6(stem,0); pos7(stem,0);
 pos8(vair,90); pos9(stem,0); pos10(vair,-90);
 x6=x7; z8l=z7l; x9=x7+1.5u; x10=x8-1.5u;
 bot y6=0; bot y7=-vround 2/7d-o; y9=.5[y8,y10]; bot y10=-d-o;
 filldraw stroke z6e--z7e;  % stem
 filldraw stroke z8e{right}...z9e{down}...{left}z10e;  % hook
else: pickup fine.nib; pos6(vair,0); top y6=-o-2;
 pos7(.5[vair,stem],0); bot y7=-d-o; x7=x6-1.25u;
 filldraw stroke z6e--z7e; fi  % diagonal
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "Lower-case O-E ligature";
beginchar(oct"367",14u#,x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(0,0);
numeric left_curve,right_curve;
if monospace: right_curve=left_curve=fudged.stem;
else: left_curve=max(fine.breadth,hround(curve-2stem_corr));
 right_curve=max(tiny.breadth,hround(curve-if serifs:6 else:8 fi\\stem_corr));fi
pickup tiny.nib; pos11(right_curve,0);
pos12(vair,90); pos13(mfudged.stem,180);
y11=good.y bar_height; top y12r=h+vround 1.5oo; y10l=bot y11;
rt x11r=hround min(w-.5u,w-u+.5right_curve);
lft x13r=hround (.5(w+u)-.5mfudged.stem); x12=.55[x13,x11];
{{interim superness:=more_super;
 filldraw stroke super_arc.e(11,12)}};  % right bowl of e
y13=.5[y12,y14]; bot y14r=-oo; x14=x12+.25u;
if serifs: pos14(vair',270); pos15(mfudged.hair,360);
 y15=max(good.y(.5bar_height-.9),y14l+vair); x15r=x11r;
 (x,y14l)=whatever[z14r,z15]; x14l:=min(x,x14l+.5u);
 filldraw stroke pulled_arc.e(12,13)
  & pulled_super_arc.e(13,14)(.8superpull)
  ...{x15-x14,5(y15-y14)}z15e; % left bowl, arc, and terminal of e
else: pos14(vair,270);
 filldraw stroke super_arc.e(12,13)
  & super_arc.e(13,14);  % left bowl and arc of e
 pickup fine.nib; pos14'(vair,270); z14=z14';
 pos15(.5[vair,flare],275); rt x15r=hround(w-.6u);
 y15r=good.y(y15r+1/3bar_height-y15); y15l:=good.y y15l; x15l:=good.x x15l;
 filldraw stroke term.e(14',15,right,1,4); fi  % terminal
y11'r=y10r=y10l+.6[thin_join,vair]; y11'l=y10l; x11'l=x11'r=x11; x10l=x10r=x13;
fill stroke z10e--z11'e;  % crossbar
pickup fine.nib; pos1(vair,90); pos3(vair,-90);
pos2(left_curve,180); pos4(mfudged.stem,0);
lft x2r=hround max(.5u,1.25u-.5curve);
z4=z13; x1=x3=.5[x2,x4]; top y1r=h+vround 1.5oo; bot y3r=-oo;
y2=y4; y2l:=y4l:=.52h;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3); % left half of left bowl
filldraw stroke pulled_arc.e(3,4) & pulled_arc.e(4,1); % right half of left bowl
penlabels(1,2,3,4,10,11,12,13,14,15); endchar;

cmchar "Slashed O";
beginchar(oct"370",CT(9u#,10u#),x_height#+.5desc_depth#,.5desc_depth#);
italcorr h#*slant-u#-max(.5fudge*stem#,.75u#)+.5vair#;
adjust_fit(0,0);
penpos1(vair,90); penpos3(vair,-90);
penpos2(fudged.stem,180); penpos4(fudged.stem,0);
x2r=hround max(.5u,1.25u-.5fudged.stem);
x4r=w-x2r; x1=x3=.5w;
y1r=x_height+vround 1.5oo; y2=y4=.5x_height-vair_corr; y3r=-oo;
penstroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
x5=x4; x6=x2; y5=h; y6=-d;
numeric theta; theta=angle(z5-z6)-90;
pickup crisp.nib; pos5(vair,theta); pos6(vair,theta);
filldraw stroke z5e--z6e;  % diagonal
penlabels(1,2,3,4,5,6); endchar;

% end of tipasym2.mf
