% tipasym1.mf: TIPA phonetic symbols (1)
% Copyright 1996-2003 FUKUI Rei
%
% This program may be distributed and/or modified under the
% conditions of the LaTeX Project Public License, either version 1.2
% of this license or (at your option) any later version.
% The latest version of this license is in
%   http://www.latex-project.org/lppl.txt
% and version 1.2 or later is part of all distributions of LaTeX 
% version 1999/12/01 or later.
%
% This program consists of all files listed in Manifest.txt.
%
% Version 1.2 2003/01/01
%
% This font is based on:
%   Computer Modern font series by D. E. Knuth and
%   TSIPA by KOBAYASHI Hajime, FUKUI Rei and SHIRAKAWA Shun.
%

cmchar "Barred U";
beginchar("0",10u#,x_height#,0);
italcorr x_height#*slant-serif_fit#+.5stem#-1.5u#;
if serifs: adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
else: adjust_fit(serif_fit#+stem_shift#+.5u#,serif_fit#-stem_shift#+.5u#); fi
numeric light_vair; light_vair=vair if hefty: -vround 2vair_corr fi;
if light_vair<fine.breadth: light_vair:=fine.breadth; fi
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
pos3(stem,0); pos4(stem',0);
lft x1l=hround(side_gap-.5stem); x1=x2; x3=w-x1; x3r=x4r;
if serifs: top y1=h if Times_Compat: else: +min(oo,serif_drop) fi;
  bot y4=-min(oo,serif_drop);
else: top y1=h; bot y4=0; fi
pos0(stem,0); pos0'(stem',0); x0=x3; x0'=x4; y0=y0';
penpos2'(stem-fine,-180); z2'=z2; y3=y1; y2=.5bar_height;
penpos5(max(eps,light_vair-fine),-90); penpos6(thin_join-fine,0);
y6=y0=2/3bar_height;
filldraw stroke z1e--z2e;  % left stem
filldraw stroke z3e..z0e--z0'e..z4e;  % right stem
pickup fine.nib; bot y5r=-oo; x5l=.5w-.25u; lft x6l=tiny.lft x0l;
(x,y5r)=whatever[z5l,z6l]; x5r:=max(x,.5[x5,x2'r]);
filldraw stroke {{interim superness:=hein_super;
 pulled_arc.e(2',5)}} & z5e{right}...{up}z6e;  % arc
if serifs: numeric upper_serif_drop, lower_serif_drop;
 if Times_Compat: upper_serif_drop=0; lower_serif_drop=serif_drop;
 else: upper_serif_drop=serif_drop; lower_serif_drop=min(oo,serif_drop); fi
 sloped_serif.l(1,2,a,1/3,jut,upper_serif_drop); % upper left serif
 sloped_serif.l(3,0,b,1/3,jut,upper_serif_drop); % upper right serif
 sloped_serif.r(4,0',c,1/3,jut,lower_serif_drop); fi % lower right serif
hbar(7,8,lft x1l - hround(1.5u),rt x3r + hround(1.5u),.58x_height);
penlabels(1,2,3,4,5); labels(6); endchar;

cmchar "Barred I";
numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
beginchar("1",CT(5u#,5.56u#),min(asc_height#,10/7x_height#+.5dot_diam#),0);
define_whole_blacker_pixels(dot_diam);
italcorr h#*slant-serif_fit# + .5dot_diam# -2u#;
if serifs: adjust_fit(serif_fit#+stem_shift#+.3u# if monospace:+.25u#fi,
 serif_fit#-stem_shift#+.3u#);
else: adjust_fit(serif_fit#+stem_shift#+.8u# if monospace:+.25u#fi,
 serif_fit#-stem_shift#+.8u#); fi
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.5w-.5stem'); x1=x2;
top y1=x_height if serifs: +min(oo,serif_drop) fi; bot y2=0;
filldraw stroke z1e--z2e;  % stem
pos3(dot_diam,0); pos4(dot_diam,90);
if serifs: x3r=max(x1r,hround(x1+.5dot_diam-.2jut)-.5tiny)
else: x3=x1-.5 fi;
top y4r=h+1;
if bot y4l-top y1<slab: y4l:=min(y4r-eps,y1+tiny+slab); fi
x3=x4; y3=.5[y4l,y4r]; dot(3,4);  % dot
if serifs: sloped_serif.l(1,2,a,1/3,.95jut,serif_drop);  % upper serif
 dish_serif(2,1,b,1/3,jut,c,1/3,.9jut); fi  % lower serif
hbar(5,6,lft x1l - hround(1.8u),rt x1r + hround(1.8u),.58x_height);
penlabels(1,2,3,4); endchar;

cmchar "Turned V";
beginchar("2",CT(if serifs: 9.5u# else:9u# fi,10u#),x_height#,0);
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
numeric left_stem,right_stem,outer_jut,alpha;
right_stem=fudged.stem-stem_corr;
left_stem=min(fudged.hair if hefty:-2stem_corr fi,right_stem);
outer_jut=.75jut; 
x1l=w-x4r=l+letter_fit+outer_jut+.25u; y1=y4=0;
x2-x1=x4-x3; x2r+apex_corr=x3r; y2=y3=h+apex_oo;
alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0<h-notch_cut: y0:=h-notch_cut;
 fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
   ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
 --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut)(dark);  % left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut); fi  % right serif
penlabels(0,1,2,3,4); endchar;

cmchar "Reversed epsilon";
beginchar("3",CT(8.5u#,8.88u#),x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(0,0);
epsilon_stroke(true,3.5u);
if serifs: numeric bulb_diam;
 bulb_diam=max(flare-.75(cap_stem-stem),stem);
 pos0(bulb_diam,180); pos1(hair,180);
 lft x0r=hround u; y0=min(.9h-.5bulb_diam,.75h+.5bulb_diam);
 bulb(2,1,0);  % upper bulb
 pos9(bot_thickness,angle(-2u,-h));
else: pos1(.47[vair',flare],100); lft x1r = 1u; top y1r = .9h;
 filldraw stroke term.e(2,1,left,1,4);
 pos9(.6[vair',flare],-100); fi
lft x9r=hround .75u; top y9=vround .25h-o;
y9r:=good.y y9r-eps; x9l:=good.x x9l;
filldraw stroke term.e(8,9,left,1,4);  % lower terminal
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "Turned H";
beginchar("4",10u#,x_height#,desc_depth#);
italcorr x_height#*slant+serif_fit#;
adjust_fit(serif_fit#-stem_shift#,serif_fit#+stem_shift#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
pos1'(stem',0); pos2'(stem',0); pos3(stem,0);
rt x1r=hround(w-(2.5u-.5stem)); x1r=x1'r=x2r=x2'r; x3=w-x1;
bot y1=-d-min(oo,serif_drop); top y2=h; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
turned_h_stroke(2,a,3,4);  % arch and right stem
if serifs: sloped_serif.r(1',2',b,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if lft x2l-jut-.5u-1>=rt x4r+jut: inner_jut=jut;
 else: lft x2l-jut-.5u-1=rt x4r+inner_jut; fi
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); % lower left serif
 dish_serif(4,3,e,1/3,inner_jut,f,1/3,jut); fi % lower right serif
penlabels(1,2,3,4); endchar;

cmchar "Turned A";
beginchar("5",CT(9u#,8.88u#),x_height#,0);
if Times_Compat: bh#:=.6x_height#;
else: bh#:=min(bar_height#,1.14x_height#-bar_height#); fi
define_pixels(bh);
italcorr .8x_height#*slant+.5stem#-1u#;
adjust_fit(serif_fit# if serifs: if hair#+.5stem#>1.5u#:-.25u# fi\\fi,0);
pickup fine.nib; bot y3r=-vround 1.5oo;
if serifs: pos1(flare,0); pos2(hair,0); pos3(vair,-90);
 rt x1r=hround(w-max(u,if Times_Compat:1.5u else:2.1u fi-.5flare));
 x3=.5w if Times_Compat:+.25u else:+.5u fi;
 y1=h-(min(bh+.5flare+2vair+2,.9[bh,h]-.5flare));
 bulb(3,2,1);  % bulb
else: pos1(5/7[vair,flare],-85); x1l=good.x(w-1.5u); x1r:=good.x x1r;
 pos3(1/8[vair,thin_join],-90);
 x3=.5w+.2u; bot y1r=vround(.18[bot y3r,h-bh]);
 filldraw stroke term.e(3,1,right,.9,4); fi  % terminal
pos4(stem,180); lft x4r=hround(side_gap-.5stem);
y4=h-if Times_Compat: .5 else: 1/3 fi[bh,h];
pos5(stem,180); x5=x4;
y5=h-if Times_Compat: max(.45bh,2vair) else: max(.55bh,2vair) fi;
filldraw stroke super_arc.e(3,4)&z4e..z5e;  % arc and stem
pos6(.3[thin_join,vair],-90); x6=x4; top y6=h-if Times_Compat: 1.1 fi bh;
pos7(hround(curve-2stem_corr),if Times_Compat: 20 else: 0 fi);
rt x7r=hround(w-max(.5u,1.5u-.5curve));
if Times_Compat: y7=.3[bot y8l,bot y6r]; pos8(.6[vair,flare],30); x8l=.5w+1u;
else: y7=1/3[bot y8l,bot y6r]; pos8(vair,90); x8l=.5w+.75u; fi
pos9(thin_join,180); z9l=z5l; top y8r=h+oo;
(x,y8r)=whatever[z8l,z9l]; x8r:=min(x,x8+u);
{{interim superness:=more_super;
 filldraw stroke z9e{up}...z8e{right}...{down}z7e &
 if Times_Compat: z7e{down}...{(-35,-10)}z6e
 else: super_arc.e(7,6)fi}}; % bowl
if serifs: numeric shaved_stem; shaved_stem=hround(stem-3stem_corr);
 if hair#+.5stem#>1.5u#: pickup tiny.nib;
  pos5'(shaved_stem,180); lft x5'r=fine.lft x5r; y5'=y5;
  pos10(shaved_stem,180); x10=x5'; y10=h-.2[.5tiny,bh];
  pos11(shaved_stem,180); lft x11r=hround .25u; top y11=h;
  pos12(shaved_stem,180); x11=x12; bot y12=h-(slab+eps);
  filldraw z5'l---z10l...z11l{left}--z11r
   --z12r{right}...z10r-.75(z12-z11)---z5'r--cycle;  % foot
 else: pickup crisp.nib; pos5'(shaved_stem,180); lft x5'r=fine.lft x5r; y5'=y5;
  pos10(shaved_stem,180); x10=x5'; y10=h-1/3bh;
  pos11(.2[vair,stem],-90); x11r=.5[x10r,x12r]; top y11l=h+vround .5oo;
  pos12(hair,0); lft x12l=hround(.1u); y12=min(y10,y11-vair);
  pos13(hair,0); x13=x12; bot y13=min(vround(h-.6bh),bot y12);
  (x',y11l)=whatever[z11r,z12r]; x11l:=min(x',x10);
  if Times_Compat: y13l:=.05[y12l,y13l]; y13r:=y13l; fi
  filldraw stroke z5'e---z10e...z11e{left}...z12e---z13e; fi  % hook
else: numeric shaved_stem; shaved_stem=hround(stem-stem_corr);
 pickup tiny.nib; pos5'(shaved_stem,180); lft x5'r=fine.lft x5r; y5'=y5;
 pos10(shaved_stem,180); x10=x5'; top y10=h;
 filldraw stroke z5'e--z10e; fi  % base of stem
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13); endchar;

cmchar "Turned script A";
beginchar("6",10u#+serif_fit#,x_height#,0);
italcorr .5x_height#*slant+min(.5curve#-u#,-.25u#);
adjust_fit(serif_fit#,0);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0l=z0'l; x0'=x1; x0=x2;
lft x1l=hround(side_gap-.5stem'); top y1=h+min(oo,serif_drop);
numeric edge; edge=rt x2r;
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,180);
pos4(vair,90); pos5(curve,0); pos6(vair,-90); penpos7(x3l-x3r,-180);
rt x3l=max(rt x3l-(lft x3r-tiny.lft x2l),1/3[rt x2,edge]);
y3=1/8[bar_height,x_height];
x4l=w-.5(w-serif_fit)+.5u; top y4r=x_height+oo;
rt x5r=hround min(w-1.35u+.5curve,w-.6u); y5=.5x_height;
x6l=x4l-.2u; bot y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=min(x,.5[x4,x5r]);
(x',y6r)=whatever[z7l,z6l]; x6r:=min(x',.5[x6,x5r]);
filldraw stroke z3e{up}...pulled_arc.e(4,5)&pulled_arc.e(5,6)...{up}z7e; % bowl
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{right}z4l));
y2=ypart(((edge,h)--(edge,0))intersectionpoint(z6l{left}...{up}z7l));
pickup tiny.nib; filldraw stroke z1e--z0'e--z0e--z2e;  % stem
pickup crisp.nib; pos8(hair,0); pos7'(stem,0);
z7'=z2; x8l=x7'l; bot y8=0;
filldraw stroke z7'e--z8e;  % point
if serifs: sloped_serif.l(1,0',a,1/3,jut,serif_drop); fi  % upper serif
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "Ram's horns";
beginchar("7",CT(9.5u#,10u#),x_height#,0);
italcorr x_height#*slant+.5hair#-.5u#;
adjust_fit(0,0); pickup fine.nib;
if serifs: pos1(hair,0); pos9(hair,180);
else: pos1(fudged.stem,-10); pos9(fudged.stem,190); fi
pos2(vair,-90); pos5(vair,90); pos8(vair,-90);
lft x1l=w-rt x9l=hround(.5u-.25hair); y1=y9=.9h-.5hair; x5=.5w; bot y5l=-o;
x2=w-x8=x1+1u+.15hair; top y2l=top y8l=h+o;
if serifs: pos4(stem,180); pos6(hair,0); pos3(stem,180);pos7(hair,0);
else: pos4(fudged.stem,180); pos6(fudged.hair,0);
 pos3(fudged.stem,180);pos7(fudged.hair,0); fi
rt x4l=hround(x5+1u+.5stem); lft x6l=hround(x5-1u-.5hair);
y4=y6=.25h+.5max(vair,.5stem);
rt x3l=lft x7l=.5w+.5(.5stem-.5hair);
y3=y7=.55h+1.5max(vair,.5stem);
filldraw stroke z3e--z4e{z4-z3}...{left}z5e...{z7-z6}z6e--z7e;
filldraw stroke z1e{up}..z2e...{z4-z3}z3e;
filldraw stroke z7e{z7-z6}...z8e..{down}z9e;
penlabels(1,2,3,4,5,6,7,8,9); endchar;

cmchar "Barred O";
beginchar("8",CT(9u#,10u#),x_height#,0);
italcorr .7x_height#*slant;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
penpos1(vair,90); penpos3(vair',-90);
penpos2(curve,180); penpos4(curve,0);
x2r=hround max(.5u,1.25u-.5curve);
x4r=w-x2r; x1=x3=.5w; y1r=h+vround 1.5oo; y3r=-oo;
y2=y4=.5h-vair_corr; y2l:=y4l:=.52h;
penstroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
hbar(5,6,x2,x4,.5x_height+.5bar);
penlabels(1,2,3,4); endchar;

cmchar "Reversed E";
beginchar("9",CT(7.25u#+max(.75u#,.5curve#),8.88u#),x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
numeric left_curve,right_curve;
left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib; pos1(right_curve,180);
pos2(vair,90); pos3(left_curve,0);
y1=good.y bar_height; top y2r=h+vround 1.5oo; y0l=bot y1;
lft x1r=hround max(.5u,u-.5right_curve);
rt x3r=hround min(w-.5u,w-(1.25u-.5left_curve)); x2=.5w-.25u;
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}};  % right bowl
y3=.5[y2,y4]; bot y4r=-oo; x4=x2-.25u;
if serifs: pos4(vair',-90); pos5(hair,-180);
 y5=max(good.y(.5bar_height-.9),y4l+vair); x5r=x1r;
 (x,y4l)=whatever[z4r,z5]; x4l:=max(x,x4l-.5u);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
  ...{x5-x4,5(y5-y4)}z5e; % left bowl, arc, and terminal
else: pos4(vair,-90);
 filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(vair,-90); z4=z4';
 pos5(.5[vair,flare],-95); lft x5r=hround(.6u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke term.e(4',5,left,1,4); fi  % terminal
path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'r=y0r=y0l+.6[thin_join,vair]; y1'l=y0l; x1'l=x1'r=x1;
forsuffixes $=l,r:
 x0$=xpart(((0,y0$)--(w,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
penlabels(0,1,2,3,4,5); endchar;

cmchar "Schwa";
beginchar("@",CT(7.25u#+max(.75u#,.5curve#),8.88u#),x_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
numeric left_curve,right_curve;
left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib; pos1(right_curve,180);
pos2(vair,-90); pos3(left_curve,0);
y1=good.y (h-bar_height); bot y2r=-vround 1.5oo; y0l=top y1;
lft x1r=hround min(.5u,u-.5right_curve);
rt x3r=hround max(w-.5u,w-1.25u+.5left_curve); x2=.5w-.25u;
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}};  % right bowl
y3=.5[y2,y4]; top y4r=h+oo; x4=x2-.25u;
if serifs: pos4(vair',90); pos5(hair,180);
 y5=min(good.y(h-(.5bar_height-.9)),y4l-vair); x5r=x1r;
 (x,y4l)=whatever[z4r,z5]; x4l:=max(x,x4l-.5u);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
  ...{x5-x4,5(y5-y4)}z5e; % left bowl, arc, and terminal
else: pos4(vair,90);
 filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(vair,90); z4=z4';
 pos5(.5[vair,flare],95); lft x5r=hround(.6u);
 y5r=good.y(h-(-y5r+1/3bar_height+y5)); y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke term.e(4',5,left,1,4); fi  % terminal
path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'r=y0r=y0l-.6[thin_join,vair]; y1'l=y0l; x1'l=x1'r=x1;
forsuffixes $=l,r:
 x0$=xpart(((w,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
penlabels(0,1,1',2,3,4,5); endchar;

cmchar "Script A";
beginchar("A",10u#+serif_fit#,x_height#,0);
italcorr asc_height#*slant-serif_fit#+.5stem#-2.5u#;
adjust_fit(0,serif_fit#);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0r=z0'r; x0'=x1; x0=x2;
rt x1r=hround(w-side_gap+.5stem'); top y1=h+oo;
numeric edge; edge=lft x2l;
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,0);
pos4(vair,90); pos5(curve,180);
pos6(vair,270); penpos7(x3r-x3l,360);
lft x3l=1/3[lft x2,edge]; y3=1/8[bar_height,x_height];
x4l=.5(w-serif_fit)-.3u; top y4r=x_height+oo;
lft x5r=hround max(1.35u-.5curve,.6u); y5=.5x_height;
x6l=x4l-.2u; bot y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=max(x',.5[x5r,x6]);
filldraw stroke z3e{up}...pulled_arc.e(4,5)
 & pulled_arc.e(5,6)...{up}z7e; % bowl
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{left}z4l));
pickup tiny.nib; bot y2=if serifs:-min(oo,serif_drop) else: 0 fi;
filldraw stroke z0'e--z0e--z2e;  % stem
pickup crisp.nib;
pos8(hround(hair-stem_corr),0); pos7'(stem',0);
z7'=z0'; x8r=x7'r; top y8=h+oo;
filldraw stroke z7'e--z8e;
if serifs:  sloped_serif.r(2,0,b,1/3,jut,serif_drop); fi  % lower serif
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "Beta";
beginchar("B",CT(9.0u#,9.5u#),asc_height#,desc_depth#);
italcorr .5[x_height#,asc_height#]*slant-u#;
adjust_fit(serif_fit#+.5u#,0); pickup tiny.nib;
pos0(stem,180); pos1(stem,180); pos2(vair,90);
pos3(stem,0); pos4(vair,-90); pos5(vair,90);
pos6(vair,-270); pos7(curve,-360); pos8(vair,-450);
if serifs: if monospace: pos9(stem,-540); else: pos9(1.25hair,-540); fi
else: pos9(stem,-540); fi
pos0'(stem,0); pos1'(stem,0); z0'=z0; z1'=z1;
x0=x1=x9; lft x0r=hround u; x4=x6=x8=.5w+.5u-.25curve; x2=.5w;
rt x3r=hround(w-1.5u); rt x7r=hround(w-1.5u+.5curve);
lft x5r=min(hround(rt x1l+1.2u),hround(x2-.3curve));
bot y0'=-d; top y4l=vround(.56asc_height+.5vair);
top y2r=h+oo; y1+o=y3=.5[y2,y4];
y5=.5[y4,y6]; y4l=y6r; bot y8=-oo; y7=y9=.5[y6,y8];
pos4'(vair,-90); pos6'(vair,-270); z4'=z6'=z5;
filldraw stroke z0e---z1e...pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{left}z4'e;  % stem and upper bowl
filldraw stroke z6'e{right}...pulled_arc.e(6,7)
 & pulled_arc.e(7,8)...{up}z9e;  % lower bowl
if serifs: dish_serif(0',1',a,1/3,jut,b,1/3,jut); fi
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Curly-tail C";
beginchar("C",CT(8u#,8.88u#),x_height#,desc_depth#);
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
numeric BH; BH=if Times_Compat:.5613h else: bar_height fi;
pickup fine.nib; pos2(vair',90); pos4(vair',270);
x2=x4=.5(w+u); top y2r=vround(h+1.5oo); bot y4r=-oo;
pos3(curve,180); lft x3r=hround max(.6u,1.35u-.5curve); y3=.5h;
if serifs: pos1(hair,0); pos0(flare,0);
 y1=min(BH+.5flare+2vair'+2,.9[BH,h]-.5flare);
 rt x1r=hround(w-.7u); bulb(2,1,0);  % bulb
 pos5(hair,0); rt x5r=hround(.92w); y5=.16h+.5vair;
 (x,y4l)=whatever[z4r,z5l]; x4l:=min(x,x4l+.5u);
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  ..tension .9 and 1..{up}z5e;  % arc and lower terminal
else: pos1(4/7[vair',flare],80);
 rt x1r=hround(w-.6u); top y1r=vround .82[BH,top y2r];
 filldraw stroke term.e(2,1,right,.8,4);  % upper terminal
 pos5(.6[vair',flare],0); x5r=hround(w-.4u);
 y5=.16h+.5vair;
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull) & z4e{right}...{up}z5e; fi
pos6(vair,100); x6l=.3[x4,x5]; y6=.3h+.5vair;
pos8(vair,180); x8=u; bot y8=-.75d;
pos7(vair,160); x7=.65[x3r,x4r]; y7=.4[y4,y6];
filldraw stroke z5e{up}...{(-8,-1)}z6e...{z8-z7}z7e...{(-1,-2)}z8e;
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "Eth";
beginchar("D",CT(9u#,10u#),asc_height#,0);
italcorr asc_height#*slant-.5u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
penpos1(vair,90); penpos3(vair',-90);
penpos2(curve,180); penpos4(curve,0);
x2r=hround max(.5u,1.25u-.5curve);
x4r=w-x2r; x1=x3=.5w; y1r=if serifs:else:.95 fi x_height; y3r=-oo;
y2=y4=.5y1r-vair_corr; y2l:=y4l:=.52x_height;
penstroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
if serifs and (not monospace): penpos5(hair/(sind 45),45);
else: penpos5(vair,80); fi
penpos4'(stem,0); x4r=x4'r; y4=y4'; top y5r=h+oo; x5=x2;
penstroke pulled_arc.e(4',5);
penpos6(bar+stem_corr,120); penpos7(bar+stem_corr,120);
y6=.35[y1,y5]; y7r=h+oo; x7r=rt x4; x6l=rt x2l;
filldraw stroke z6e--z7e;
penlabels(1,2,3,4,5,6,7); endchar;

cmchar "Epsilon";
beginchar("E",CT(8.5u#,8.88u#),x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(0,0);
epsilon_stroke(false,3.5u);
if serifs: numeric bulb_diam;
 bulb_diam=max(flare-.75(cap_stem-stem),stem);
 pos0(bulb_diam,0); pos1(hair,0);
 rt x0r=hround (w-u); y0=min(.9h-.5bulb_diam,.75h+.5bulb_diam);
 bulb(2,1,0);  % upper bulb
 pos9(bot_thickness,angle(2u,-h));
else: pos1(.47[vair',flare],80); rt x1r = w-1u; top y1r = .9h;
 filldraw stroke term.e(2,1,right,1,4);
 pos9(.6[vair',flare],-80); fi
rt x9r=hround (w-.75u); top y9=vround .25h-o;
y9r:=good.y y9r-eps; x9l:=good.x x9l;
filldraw stroke term.e(8,9,right,1,4);  % lower terminal
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "Phi";
beginchar("F",11u#,asc_height#,desc_depth#);
italcorr .7x_height#*slant+.5curve#-1.25u#;
adjust_fit(0,0); pickup tiny.nib;
numeric light_curve; light_curve=hround(.5[stem,curve]-2stem_corr);
pos1(vair,90); pos2(light_curve,180); pos3(vair,270); pos4(light_curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5light_curve); x4=w-x2;
top y1r=min(.75h,x_height+2oo); bot y3r=h-top y1r-d-oo;
y2=y4=.5[y1,y3];
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1); % bowl
pos5(stem,0); pos6(stem,0); x5=x6=.5w; top y5=h; bot y6=-d;
filldraw stroke z5e--z6e;  % stem
if serifs: dish_serif(5,6,a,1/3,jut,b,1/3,jut);
 dish_serif(6,5,c,1/3,jut,d,1/3,jut); fi
penlabels(1,2,3,4,5,6); endchar;

cmchar "Gamma";
beginchar("G",CT(if serifs: 9.5u# else:9u# fi,10u#),x_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
numeric left_stem,right_stem,outer_jut,alpha,raise;
left_stem=fudged.stem-stem_corr;
right_stem=min(fudged.hair if hefty:-2stem_corr fi,left_stem);
outer_jut=.75jut; x1l=w-x4r=l+letter_fit+outer_jut+.25u; y1=y4=h;
raise=if serifs: .05x_height; else: .08x_height; fi
x2-x1=x4-x3; x2l+apex_corr=x3l; y2=y3=raise;
alpha=diag_ratio(2,right_stem,y1-y2,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0>notch_cut+raise: y0:=notch_cut+raise;
 fill z0+.5right{up}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
   ...{down}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
 --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut);  % left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark); fi  % right serif
pickup penrazor;
penpos5(alpha*left_stem,0); penpos7(vair,-90); penpos9(alpha*right_stem,180);
y5=y9=y0; z5=whatever[z1,z2]; z9=whatever[z4,z3];
y6=y8=-.5d if not serifs:+.5vair fi;
z6=whatever[z1,z2]; z8=whatever[z4,z3];
bot y7r=-d-o; x7=.5w+.2(left_stem-right_stem);
numeric stem_jut[], y_shift, circle_gap;
circle_gap=(x6-.5left_stem)-(x8+.5right_stem);
stem_jut1=if circle_gap<.5u: .5 else: .15 fi left_stem;
stem_jut2=if circle_gap<.5u: .5 else: .15 fi right_stem;
y_shift=if (left_stem-1.2right_stem)>0: .25d else: 0 fi;
x6:=x6+stem_jut1; x8:=x8-stem_jut2; y8:=y8+y_shift;
penpos6(left_stem,0); penpos8(right_stem,180);
filldraw stroke z5e{z2-z1}..z6e..{left}z7e..z8e..{z4-z3}z9e; % bottom loop
if not serifs: % minor adjustments
 pos5'(alpha*left_stem,0);pos9'(alpha*right_stem,180);
 y5'=y9'=0; z5'l=whatever[z1l,z2l]; z9'l=whatever[z4r,z3r];
 fill z5l--z5'l--z5'--z5--cycle;
 fill z9l--z9'l--z9'--z9--cycle; fi
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Hooktop H";
beginchar("H",10u#,asc_height#,0);
italcorr .7[x_height#,asc_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
pos1'(stem',0); pos2'(stem',0); pos3(stem,0);
lft x1l=hround(side_gap-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=.25[x_height,h]; bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,b,3,4);  % arch and right stem
hooktop(1,9,10,11,stem',hround(w-1.5u),h,.8,.55,.2);
if serifs: numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); % lower left serif
 dish_serif(4,3,e,1/3,inner_jut,f,1/3,jut); fi % lower right serif
penlabels(1,2,3,4,9,10,11); endchar;

cmchar "Curly-tail J";
numeric dot_diam#; dot_diam#=max(dot_size#,cap_curve#);
beginchar("J",CT(5.5u#,6.66u#),min(asc_height#,10/7x_height#+.5dot_diam#),
 desc_depth#);
define_whole_blacker_pixels(dot_diam);
italcorr h#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+2stem_shift# if monospace:+\\.5u# fi +.5u#,
 -2stem_shift# if monospace:-.5u# fi +.3u#);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
rt x1r=hround(.5w+.25u+.5stem'); x1=x2;
top y1=x_height if serifs: +min(oo,serif_drop) fi; bot y2=-1/3d;
filldraw stroke z1e--z2e;  % stem
pos3(dot_diam,0); pos4(dot_diam,90);
x3r=x1r; top y4r=h+1;
if bot y4l-top y1<slab: y4l:=min(y4r-eps,y1+tiny+slab); fi
x3=x4; y3=.5[y4l,y4r]; dot(3,4);  % dot
if serifs: sloped_serif.l(1,2,a,1/3,1.1jut,serif_drop);  % upper serif
 pickup tiny.nib; pos5(vair,-90); pos6(hair,-180);
 pos7(vair,-270); pos8(vair,-270);
 x5=.5[x2,x6r]; bot y5r=-d-oo; y6=-.6d;
 if monospace: lft x6r=0 else: x6r=-.3u fi;
 (x,y5r)=whatever[z5l,z2l]; x5r:=max(x,.5[x6r,x5]);
 y7r=y8r=0h; x7=x2l-.5u; x8=w-.5u;
 filldraw stroke z2e{down}...z5e{left}...z6e{up}...z7e{right}---z8e;
else: pickup fine.nib; pos2'(stem',0); z2'=z2;
 pos6(.2[vair,stem'],-90); pos7(vair,-180);
 pos8(vair,-270); pos9(vair,-270);
 lft x7r=hround -.75u; y7=-.5d;
 z5r=z2'r; (x2'l,y5l)=whatever[z7l,z5r]; x5l=x2'l; y5=y5r;
 x6r=.5[x7r,x5r]; x6l:=.5[x7l,x5l]; bot y6r=-d-oo;
 bot y8l = bot y9l = vround(-.5vair); x8 = .5[x7,x2]; x9 = w;
 filldraw stroke z2'e..{down}z5e & super_arc.e(5,6)
  & z6e{left}..z7e...{right}z8e--z9e; fi  % arc and terminal
penlabels(1,2,3,4,5,6,7,8); endchar;

cmchar "Turned Y";
beginchar("L",CT(if serifs:9.5u# else:9u# fi,10u#),x_height#+desc_depth#,0);
italcorr (x_height#+.6desc_depth#)*slant+.25u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
numeric left_stem,right_stem,bot_stem,bot_vair,outer_jut;
right_stem=fudged.stem-stem_corr;
left_stem=fudged.hair if hefty:-2stem_corr fi;
bot_stem=fudged.hair if hefty:-8stem_corr fi;
bot_vair=Vround(if serifs: vair else:.5[vair,bot_stem] fi);
outer_jut=.75jut;
x1l=w-x4r=l+letter_fit+outer_jut+.25u; y1l=y4r=0; y2r=y3=x_height; x2r=x3r;
numeric alpha,alpha[]; x9=w-3u; y9=h+oo-bot_vair;
alpha1=diag_ratio(2,bot_stem,y3-y1l,x4r-x1l-apex_corr);
alpha2=diag_ratio(1,bot_stem,y9-y1l,x4r-x9);
if alpha1>=alpha2: x4r-x3r=x2l-x1l+apex_corr; alpha=alpha1;
else: alpha=alpha2; z2r=whatever[z9,z1l-(alpha*bot_stem,0)]; fi
penpos1(alpha*left_stem,0); penpos2(alpha*bot_stem,0);
alpha3=(y3++(x4r-x3r))/y3;
penpos3(alpha3*right_stem,0); penpos4(alpha3*right_stem,0);
z0=whatever[z4l,z3l]=z1r+whatever*(z2l-z1l);
if y0<x_height-notch_cut: y0:=x_height-notch_cut;
  fill z0+.5right{down}...{z4r-z3r}diag_end(0,4l,1,1,4r,3r)
    --z3r--z2l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
    ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--z3r--z2l
    --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(alpha*bot_stem,0); z5r=whatever[z2r,z1r];
y5+.5vair=x_height+.5desc_depth;
if serifs: numeric light_bulb; light_bulb=hround 7/8[hair,flare]; clearpen;
 penpos6(vair,-90); penpos7(hair,0); penpos8(light_bulb,0);
 penpos6'(vair,90);z6'=z6;
 x6=w-2u; y6l=h+oo; y8+.5light_bulb=x_height+.85desc_depth; x8r=hround(w-.35u);
 fill stroke z2e---z5e...{right}z6e; bulb(6',7,8);  % arc and bulb
 numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/2,outer_jut,b,.6,inner_jut)(dark);  % left serif
 dish_serif(4',3,c,1/2,inner_jut,d,1/3,outer_jut);  % right serif
else: penpos6(bot_vair,-90); x6=w-2.5u; y6l=h+oo;
 fill stroke z2e---z5e...{right}z6e;  % arc
 pickup fine.nib; pos6'(bot_vair,-90); z6'=z6;
 pos7(2/3[bot_vair,flare],-85);
 rt x7r=hround(w-u); top y7l=x_height+vround(.96desc_depth)+oo;y7r:=good.y y7r;
 filldraw stroke term.e(6',7,right,1,4); fi % arc and terminal
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Left-tail M (at right)";
beginchar("M",CT(15u#,15.56u#),x_height#,desc_depth#);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-1.75u#;
adjust_fit(serif_fit#+stem_shift#,-stem_shift# if serifs:-.5u# fi);
numeric shaved_stem; shaved_stem=hround(mfudged.stem-2stem_corr);
pickup tiny.nib; pos1(mfudged.stem,0); pos2(mfudged.stem,0);
pos1'(shaved_stem,0); pos2'(shaved_stem,0);
pos3(mfudged.stem,0); pos5(mfudged.stem,0);
lft x1l=hround(side_gap-.5stem); x1l=x1'l=x2l=x2'l;
lft x3l=hround(.5w-.5stem); x5-x3=x3-x1;
if not monospace: r:=hround(x5+x1)-l; fi  % change width for better fit
top y1=h+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % left arch and middle stem
h_stroke(4,b,5,6);  % right arch and right stem
if serifs: sloped_serif.l(1',2',c,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(2,1,d,1/3,jut,e,1/3,jut); % lower left serif
 dish_serif(4,3,f,1/3,inner_jut,g,1/3,jut); fi % lower middle serif
left_tail(6,7,8,9,mfudged.stem,rt x4r);
penlabels(1,2,3,4,5,6,7,8,9); endchar;

cmchar "Eng";
beginchar("N",10u#,x_height#,desc_depth#);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-1.25u#;
adjust_fit(serif_fit#+stem_shift#,-stem_shift# if serifs:-.5u# fi);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
lft x1l=hround(side_gap-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=h+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % arch and right stem
if serifs: sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); fi % lower left serif
left_tail(4,5,6,7,stem,rt x2r);
penlabels(1,2,3,4,5,6,7); endchar;

cmchar "Open O";
beginchar("O",CT(8u#,8.88u#),x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
numeric BH; BH=if Times_Compat:.5613h else: bar_height fi;
pickup fine.nib; pos2(vair',-90); pos4(vair',90);
x2=x4=.5(w-u); bot y2r=vround(-1.5oo); top y4r=h+oo;
pos3(curve,0); rt x3r=hround min(w-.6u,w-1.35u+.5curve); y3=.5h;
if serifs: pos1(hair,-180); pos0(flare,-180);
 y1=max(h-BH-.5flare-2vair'-2,h-.9[BH,h]+.5flare);
 lft x1r=hround(.7u); bulb(2,1,0);  % bulb
 pos5(hair,-180); lft x5r=hround(.5u);
 y5=min(h-good.y(.5BH-.9),y4l-vair');
 (x,y4l)=whatever[z4r,z5l]; x4l:=x;
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
   ..tension .9 and 1..{x5-x4,5(y5-y4)}z5e;  % arc and lower terminal
else: pos1(4/7[vair',flare],-100);
 lft x1r=hround(.6u); bot y1r=h-vround .82[BH,top y4r];
 filldraw stroke term.e(2,1,left,.8,4);  % upper terminal
 pos5(.6[vair',flare],95); lft x5r=hround .5u;
 y5r=good.y(h+y5r-1/3BH-y5);
 y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
   ..tension .9 and 1..z5e; fi  % arc and lower terminal
penlabels(0,1,2,3,4,5); endchar;

cmchar "Glottal stop";
beginchar("P",CT(9u#,10u#),asc_height#,0);
italcorr .8asc_height#*slant-.5u#;
adjust_fit(0,0);
pickup tiny.nib; pos7(stem,0); bot y7=0; x7=.47w;
pos2(vair,90); pos3(curve,0);
pos4(vair,-90); pos5(stem,0); pos6(stem,0);
x2=x5=x6=x7; x4=x5l;
rt x3r=hround(w-u); bot y6=.3x_height;
top y2r=h+oo; y3=.5[y2,y4]; y4r=.5y2; y5=y4l;
{{interim superness:=more_super;
 filldraw stroke pulled_super_arc.e(2,3)(superpull)
  & z3e{down}...{(-10,-1)}z4e;
 filldraw stroke z5e--z7e\\}};  % arc and stem
if serifs: pos1(hair,180); pos0(flare,180);
 lft x1r=hround u; y1=.3[y3,y2]; bulb(2,1,0);  % bulb
 dish_serif(7,5,e,1/3,jut,f,1/3,jut); %serif
else: pickup fine.nib; pos2'(vair,90); z2'=z2;
 pos1(vround 5/7[vair,flare],110);
 lft x1r=hround u; top y1r=vround .9[y6,top y2r];
 filldraw stroke term.e(2',1,left,1,4); fi  % terminal
penlabels(0,1,2,3,4,5,6,7); endchar;

cmchar "Reversed glottal stop";
beginchar("Q",CT(9u#,10u#),asc_height#,0);
italcorr .8asc_height#*slant-.5u#;
adjust_fit(0,0);
pickup tiny.nib; pos7(stem,0); bot y7=0; x7=.53w;
pos2(vair,90); pos3(curve,180);
pos4(vair,-90); pos5(stem,0); pos6(stem,0);
x2=x5=x6=x7; x4=x5r;
lft x3r=hround(u); bot y6=.3x_height;
top y2r=h+oo; y3=.5[y2,y4]; y4r=.5y2; y5=y4l;
{{interim superness:=more_super;
 filldraw stroke pulled_super_arc.e(2,3)(superpull)
  & z3e{down}...{(10,-1)}z4e;
 filldraw stroke z5e--z7e\\}};  % arc and stem
if serifs: pos1(hair,0); pos0(flare,0);
 rt x1r=hround (w-u); y1=.3[y3,y2]; bulb(2,1,0);  % bulb
 dish_serif(7,5,e,1/3,-jut,f,1/3,-jut); %serif
else: pickup fine.nib; pos2'(vair,90); z2'=z2;
 pos1(vround 5/7[vair,flare],70);
 rt x1r=hround (w-u); top y1r=vround .9[y6,top y2r];
 filldraw stroke term.e(2',1,right,1,4); fi  % terminal
penlabels(0,1,2,3,4,5,6,7); endchar;

cmchar "Fish-hook R";
numeric r_flare#; r_flare#=.75[if serifs: stem# else: vair# fi,flare#];
define_whole_blacker_pixels(r_flare);
beginchar("R",CT(if serifs:max(7u#,5.5u#+r_flare#) else:6.5u# fi,6.66u#),
 x_height#,0);
italcorr x_height#*slant if not serifs: +.25u# fi;
adjust_fit(serif_fit#,0);
pickup fine.nib; top y4r=h+oo;
if serifs: pos4(vair,90); pos5(hair,0); x4=.5[x6,.5w];
 rt x5r=hround(w-if Times_Compat: .25u else: .5u fi +.5);
 y5+.8r_flare=.9[bar_height,h]+oo;
 pos6(r_flare,0); bulb(4,5,6);  % bulb
else: pos4(r_flare,90); rt x4=hround(w-.25u); fi
pos0'(hround(stem-3stem_corr),180); 
rt x0'l=hround(side_gap-.5stem')+stem'; top y0'=bar_height+1.8u;
filldraw stroke z0'e{up}...{right}z4e;  % arc
pickup tiny.nib; pos0(stem',0); pos2(stem',0);
pos1(hround(stem-3stem_corr),0); top y1=h+min(oo,serif_drop);
y0=y0'; x1l=x0l=x2l;
lft x1l=hround(side_gap-.5stem'); bot y2=0;
filldraw stroke z0e--z2e;  % stem
if serifs: dish_serif(2,0,b,1/3,jut,c,1/3,1.25jut); fi  % lower serif
penlabels(1,2,0',4,5,6); endchar;

cmchar "Esh";
beginchar("S",CT(5u#,5.56u#),asc_height#,desc_depth#);
italcorr asc_height#*slant+1u#;
adjust_fit(.5u# if monospace:+1.5u# fi,.5u# if monospace:+1.5u# fi);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
lft x1l=hround(.5w-.5stem'); x2=x1; y2=0; h-y1=d;
filldraw stroke z1e--z2e; % stem
left_tail(2,3,4,5,stem',-1u);
hooktop(1,6,7,8,stem',w+1u,h,.9,.5,1/3);
penlabels(1,2,3,4,5,6,7,8); endchar;

cmchar "Theta";
beginchar("T",CT(8.25u#,8.88u#),asc_height#,0);
italcorr .7asc_height#*slant+.5curve#-u#;
adjust_fit(0,0); pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1);  % bowl
pos2'(bar,90); pos4'(bar,90); z2'=z2l; z4'=z4l;
filldraw stroke z2'e--z4'e;  % bar
penlabels(1,2,3,4); endchar;

cmchar "Upsilon";
beginchar("U",CT(10u#,11u#),x_height#,0);
italcorr x_height#*slant;
adjust_fit(0,0);
pickup tiny.nib; pos1(vair,-90); pos2(curve,180);
pos3(vair,180); pos4(curve,0); pos5(vair,0);
x1=.5w; bot y1r=-o; lft x2r=hround 1.25u; y2=y4=.45h; x4=w-x2;
rt x3l=hround(1/3(w+.5u)+.25hair); top y3=top y5=h; x5=w-x3;
filldraw stroke z3e{down}...{down}z2e
 & pulled_super_arc.e(2,1)(.5superpull)
 & pulled_super_arc.e(1,4)(.5superpull)
 & z4e{up}...{up}z5e;  % bowl
numeric arm_thickness[]; path p; p=z3{down}...{down}z2;
arm_thickness1=vround(if hefty:slab+2stem_corr else: min(1.5slab,vstem) fi);
arm_thickness2=if (not serifs) or monospace: arm_thickness1;
else: vround(if hefty:.5slab+2stem_corr else: min(slab,.5vstem) fi); fi
pickup crisp.nib; pos6(arm_thickness1,90); pos7(arm_thickness2,90);
top y6r=top y7r=h; x6=x3; lft x7r=hround 1.0u;
(x,y)=p intersectionpoint((0,y6l)--(w,y6l)); x6l:=x;
filldraw stroke z6e--z7e;
pos8(arm_thickness1,90); pos9(arm_thickness2,90);
y8=y6; y9=y7; x8+x6=x9+x7=w; x8l:=w-x6l;
filldraw stroke z8e--z9e;
penlabels(1,2,3,4,5,6,7,8,9); endchar;

cmchar "Script V";
beginchar("V",CT(9u#,9u#),x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(serif_fit#+stem_shift#,stem_shift#);
pickup tiny.nib; interim superness:=more_super;
pos3(stem,-180); lft x3r=hround(side_gap-.5stem);
x0=0; x2=x3; top y2=h+min(oo,serif_drop);
pos2'(stem,-180); z2'=z2; pos4(vair,-90); pos5(hair,0);
x4=.5w; rt x5r=hround(w-.8u); y3=.5[y4,y5]; bot y4r=-oo; y5=.57h;
filldraw stroke z2'e{down}--super_arc.e(3,4)
 ..tension atleast 1.05..{up}z5e; % left stem and arc
if serifs: v_bulb(5,6);  % closing bulb
 pos7(stem,0); pos8(stem,0); z7=z2; z8=z3;
 sloped_serif.l(7,8,a,1/3,jut,serif_drop);
else: pos6(hair,15); top y6=h; x6=x5-.5u;
 filldraw stroke z5e{up}..z6e; fi
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "Turned M";
beginchar("W",CT(15u#,15.56u#),x_height#,0);
italcorr x_height#*slant+serif_fit#;
adjust_fit(serif_fit#-stem_shift#,serif_fit#+stem_shift#);
numeric shaved_stem; shaved_stem=hround(mfudged.stem-2stem_corr);
pickup tiny.nib; pos1(mfudged.stem,0); pos2(mfudged.stem,0);
pos1'(shaved_stem,0); pos2'(shaved_stem,0);
pos3(mfudged.stem,0); pos5(mfudged.stem,0);
rt x1r=hround(w-(2.5u-.5stem)); x1r=x1'r=x2r=x2'r;
rt x3r=hround(.5w+.5stem); x5-x3=x3-x1;
if not monospace: r:=hround(x5+x1)-l; fi  % change width for better fit
bot y1=-min(oo,serif_drop); top y2=h; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
turned_h_stroke(2,a,3,4);  % left arch and middle stem
turned_h_stroke(4,b,5,6);  % right arch and right stem
if serifs: sloped_serif.r(1',2',c,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if lft x2l-jut-.5u-1>=rt x4r+jut: inner_jut=jut;
 else: lft x2l-jut-.5u-1=rt x4r+inner_jut; fi
 dish_serif(2,1,d,1/3,jut,e,1/3,jut); % lower left serif
 dish_serif(4,3,f,1/3,inner_jut,g,1/3,jut); % lower middle serif
 dish_serif(6,5,h,1/3,inner_jut,i,1/3,jut); fi % lower right serif
penlabels(1,2,3,4,5,6); endchar;

cmchar "Chi";
beginchar("X",CT(9.5u#,10u#),x_height#,desc_depth#);
italcorr x_height#*slant+.5hair#-.75u#;
adjust_fit(0,0); pickup fine.nib;
if serifs: pos1(hair,180); pos6(hair,180);
 y1=y2l-h/6; y6=y5r+h/6;
else: pos1(fudged.hair,130); pos6(fudged.hair,130);
 y1=y2l-h/8; y6=y5r+h/8; fi
pos2(curve,80); pos5(curve,80);
x1-.5hair=hround-.5hair+.5u; x6=w-x1; x2=w-x5=2u;
top y2r=h+oo; bot y5l=-d-oo;
z0=.5[z2,z5]; z2'=z2l+(.5u,0); z5'=z5r-(.5u,0);
numeric theta; theta=angle(z5'-z2')+90;
if serifs: pos3(hair,theta); pos4(hair,theta);
else: pos3(fudged.hair,theta); pos4(fudged.hair,theta); fi
y3r=y2r-h/3; y4l=y5l+h/3; %z3l=whatever[z2',z0]; z4r=whatever[z0,z5'];
if serifs: x3r+u=x4l-u=.5w;
else: x3r+.4u=x4l-.4u=.5w; fi
filldraw stroke z1e{up}..z2e{right}..z3e{z4r-z3r}
 ..z4e{z4r-z3r}..{right}z5e..{up}z6e;  % major diagonal and hooks
x8l=w-x7r=u; top y7=h; bot y8=-d;
if serifs: pos7(stem,0); pos8(stem,0);
else: pos7(fudged.stem,0); pos8(fudged.stem,0); fi
filldraw stroke z7e--z8e;  % minor diagonal
penlabels(0,1,2,2',3,4,5',5,6,7,8); endchar;

cmchar "Yogh";
beginchar("Z",CT(8u#,8.88u#),x_height#,desc_depth#);
italcorr x_height#*slant-.5serif_fit#-.3u#;
adjust_fit(0,.5serif_fit#);
yogh_stroke(.5u,.3,.75u,false,false);
penlabels(1,2,3,4,5,6,7,8,9,10,12); endchar;

cmchar "Pipe";
beginchar("|",CT(5u#,5.56u#),asc_height#,desc_depth#);
italcorr asc_height#*slant+.5rule_thickness#-2u#;
adjust_fit(0,0);
numeric thickness; thickness=hround.4[hair,stem];
pickup tiny.nib; pos1(thickness,0); pos2(thickness,0);
lft x1l = lft x2l = hround(.5w-.5thickness);
top y1=h; bot y2=-d;
filldraw stroke z1e--z2e;
penlabels(1,2); endchar;

cmchar "Double pipe";
beginchar("{",CT(8u#,8.88u#),asc_height#,desc_depth#);
italcorr asc_height#*slant+.5rule_thickness#-2u#;
adjust_fit(0,0);
numeric thickness; thickness=hround(.4[hair,stem]-2stem_corr);
pickup tiny.nib; pos1(thickness,0); pos2(thickness,0);
pos3(thickness,0); pos4(thickness,0);
x1=x2; x3=x4=w-x1; lft x1l = hround((w-3.2u-thickness)/2);
top y1=top y3=h; bot y2=bot y4=-d;
filldraw stroke z1e--z2e; filldraw stroke z3e--z4e;
penlabels(1,2,3,4); endchar;

cmchar "Double-barred pipe";
beginchar("}",10u#,asc_height#,desc_depth#);
italcorr asc_height#*slant+.5rule_thickness#-1.5u#;
adjust_fit(0,0);
numeric thickness[]; thickness1=hround.4[hair,stem];
pickup tiny.nib; pos1(thickness1,0); pos2(thickness1,0);
lft x1l = lft x2l = hround(.5w-.5thickness1);
top y1=h; bot y2=-d;
filldraw stroke z1e--z2e;
thickness2=vround .3[vair,flare];
pos3(thickness2,90); pos4(thickness2,90);
pos5(thickness2,90); pos6(thickness2,90);
lft x3l = lft x5l = hround 1u;
rt x4l = rt x6l = hround(w-1u);
top y3r = top y4r = h-d-bot y5;
bot y5l = bot y6l = vround(.4(h+d)-d-.5thickness2);
filldraw stroke z3e--z4e;
filldraw stroke z5e--z6e;
penlabels(1,2,3,4,5,6); endchar;

% end of tipasym1.mf
